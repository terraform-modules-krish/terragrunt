# Unusual problem with terragrunt module

**kiklop74** commented *Oct 11, 2021*

I have a custom terraform module that does not require any input variables (just works with configured aws provider) and generates specific output. What I would like is to take that in terragrunt module and generate a file. But not sure how to do it.

This is the aws terraform module

```lang-hcl
data "aws_vpc" "main" {
  default = true
}

data "aws_subnets" "vpcsubnets" {
  filter {
    name   = "vpc-id"
    values = [data.aws_vpc.main.id]
  }
  filter {
    name   = "default-for-az"
    values = [true]
  }
  filter {
    name   = "state"
    values = ["available"]
  }
}

data "aws_subnet" "vpcsubnet" {
  for_each = toset(data.aws_subnets.vpcsubnets.ids)
  id       = each.value
}

locals {
  filtered_subnets1 = [
  for v in data.aws_subnet.vpcsubnet : v if v.available_ip_address_count > 20
  ]
  filtered_subnets2 = [
  for v in data.aws_subnet.vpcsubnet :
  v if !contains([
    "us-west-2d", "ca-central-1d", "sa-east-1b"
  ], v.availability_zone)
  ]
  filtered_subnets  = tolist(setintersection(local.filtered_subnets1, local.filtered_subnets2))
  final_subnet      = element(local.filtered_subnets, 0)
  config_content    = jsonencode({
    default_subnet_id         = local.final_subnet.id
    default_availability_zone = local.final_subnet.availability_zone
  })
}

output "configtpl" {
  description = "Config content"
  value       = local.config_content
}
```

I have a terragrunt structure like this:

```
aws
  |
  ------ awsmodules
  |            |
  |            ---- subnet
  |                      |
  |                      ---- main.tf
  |
  ------ terragrunt
               |
               |
              terragrunt.hcl
```

This is the root terragrunt.hcl

```lang-hcl

# boilerplate stuff snipped (provider config, version constraints etc.)

terraform {
  source = "${get_terragrunt_dir()}/../..//awsmodules/subnet"
}

generate "subnet-data" {
  path              = "subnet.auto.tfvars.json"
  if_exists         = "skip"
  disable_signature = true
  contents          = outputs.configtpl # how to refer to the outputs within the same file?
}

```

<br />
***


**denis256** commented *Oct 11, 2021*

Hello,
most probably `dependency` block may help here https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#dependency

After declaration of dependency, can be accessed outputs, example:
```
$ tree
.
├── awsmodules
│   └── tomato
│       ├── main.tf
│       └── terragrunt.hcl
├── README.md
└── terragrunt
    ├── main.tf
    └── terragrunt.hcl

$ cat terragrunt/terragrunt.hcl

dependency "awsmodules_tomato" {
  config_path = "../awsmodules/tomato"
}

generate "subnet-data" {
  path              = "subnet.auto.tfvars.json"
  if_exists         = "skip"
  disable_signature = true
  contents          = jsonencode(dependency.awsmodules_tomato.outputs.test_value)
}

$ cat awsmodules/tomato/main.tf

resource "local_file" "local_file" {
    content     = "Local work done by module"
    filename = "${path.module}/work.txt"
}

output "test_value" {
  value = {
    tomato = 666
  }
}

$ cd terragrunt
$ terragrunt run-all apply
$ cat subnet.auto.tfvars.json
{"tomato":666}
```

Full example:
https://github.com/denis256/terragrunt-tests/tree/master/issue-1855
***

