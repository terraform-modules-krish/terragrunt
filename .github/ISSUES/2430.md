# Unable to import multiple Terraform Sources

**NicolaSpreafico** commented *Jan 26, 2023*

Hello,

 I have some "main" modules which defines the infrastructure and some "commons" module meaning that are outsources structures imported by each module so that the definition is shared between them. Those module are locals and not sourced via repository, so there is a sub-folder dedicated to them

Here is the main structure:
![image](https://user-images.githubusercontent.com/12278553/214836939-7ec17ce6-6a16-46f1-9613-244126810b92.png)

Content of each file:

**live/prod/module1/terragrunt.hcl**
**live/prod/module2/terragrunt.hcl**
```
include "root" {
  path = "../../root.hcl"
}
```

**live/root.hcl**
```
locals {
  parsed = regex(".*/live/(?P<env>.*?)/(?P<module>.*?)$", get_original_terragrunt_dir())
  env    = local.parsed.env
  module = local.parsed.module
}

remote_state {
  backend = "gcs"

  config = {
    bucket = "my-beautiful-bucket"
    prefix = local.module
  }

  generate = {
    path      = "backend.tf"
    if_exists = "overwrite_terragrunt"
  }
}


terraform {
  source = "${get_original_terragrunt_dir()}/../../../modules/${local.module}///"
}
```

These are the "main" modules
**modules/module1/main.tf**
**modules/module2/main.tf**
```
module "m1" {
    source = "../_shared_modules/terraform_shared_modules_1"
}

module "m2" {
    source = "../_shared_modules/terraform_shared_modules_2"
}

do something with the modules...
```

**modules/_shared_modules/terraform_shared_modules_1/main.tf**
**modules/_shared_modules/terraform_shared_modules_2/main.tf**
```
create something with terraform...
```

You can see that both of the shared modules are called by each one of "main" modules.

During the terragrunt plan I get this error
```
> terragrunt plan

Initializing modules...
- m1 in 
- m2 in 
╷
│ Error: Unreadable module directory
│ 
│ Unable to evaluate directory symlink: lstat ../_shared_modules: no such
│ file or directory
╵

╷
│ Error: Failed to read module directory
│ 
│ Module directory  does not exist or cannot be read.
```

I understand that this error is generated by the fact that when Terragrunt retrieve the Terraform source (as they are defined by the `terraform` block) it copies the structure to an internal workbench, so that the relative paths are not working anymore.

At the moment I find the solution of changing the relative paths to start from the terragrunt workbench, which is many folder above
```
module "m1" {
    source = "../../../../../../modules/_shared_modules/terraform_shared_modules_1"
}

module "m2" {
    source = "../../../../../../modules/_shared_modules/terraform_shared_modules_2"
}
```
and the `plan` goes fine
```
terragrunt plan
Initializing modules...
- m1 in ../../../../../../modules/_shared_modules/terraform_shared_modules_1
- m2 in ../../../../../../modules/_shared_modules/terraform_shared_modules_2

Initializing the backend...

Successfully configured the backend "gcs"! Terraform will automatically
use this backend unless the backend configuration changes.

Initializing provider plugins...

Terraform has been successfully initialized!

You may now begin working with Terraform. Try running "terraform plan" to see
any changes that are required for your infrastructure. All Terraform commands
should now work.

If you ever set or change modules or backend configuration for Terraform,
rerun this command to reinitialize your working directory. If you forget, other
commands will detect it and remind you to do so if necessary.

No changes. Your infrastructure matches the configuration.

Terraform has compared your real infrastructure against your configuration
and found no differences, so no changes are needed.
```

The problem is, I don't think it is the proper solution to solve this kind of scenario.
At first I tried to simply add multiple source, something like:
```
terraform {
  source = "${get_original_terragrunt_dir()}/../../../modules/${local.module}///"
}

terraform {
  source = "${get_original_terragrunt_dir()}/../../../modules/_shared_modules/terraform_shared_modules_1"
}
```
But I understand that this is not possible
```
ERRO[0000] /mnt/c/Users/nicol/Desktop/terragrunt_test/live/root.hcl:26,1-10: Duplicate terraform block; Only one terraform block is allowed. Another was defined at /mnt/c/Users/nicol/Desktop/terragrunt_test/live/root.hcl:22,1-10. 
ERRO[0000] Unable to determine underlying exit code, so Terragrunt will exit with error code 1 
```

Considering the scenario I shown, is the the only/proper way to do what I done with the multiple upper hierarchy or is there a better way to do that?

Thanks
<br />
***


**denis256** commented *Jan 26, 2023*

Hi,
in some of my projects, I use `get_repo_root()` function to have a predefined path from where to reference modules

https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#get_repo_root
***

**NicolaSpreafico** commented *Jan 26, 2023*

Hello,
the `get_repo_root()` function is Terragrunt based, while in my case the moduled is imported as pure Terraform.

Does your suggestion applies to this scenario?
***

**denis256** commented *Jan 26, 2023*

Hi,
depending on the modules complexity, can be generated Terraform code using Terragrunt functions:
```
# terragrunt.hcl
...
generate "modules" {
  path      = "modules.tf"
  if_exists = "overwrite"
  contents = <<EOF
module "m1" {
    source = "${get_repo_root()}/modules/m1"
}

module "m2" {
    source = "${get_repo_root()}/modules/m2"
}

EOF
}
...
```
***

**NicolaSpreafico** commented *Jan 26, 2023*

Hi,
I was aware of this solution but because my modules has variables I'm unable to generate it like this. Unless there si another way I can pass the variable list somehow
***

**denis256** commented *Jan 26, 2023*

Hm, can be used terraform hooks to copy modules directory and later reference from current directory:

```
# terragrunt.hcl
terraform {

  before_hook "before" {
    commands     = ["init", "apply", "destroy"]
    execute      = ["cp", "-rfv", "${get_repo_root()}/modules", "."]
  }

  after_hook "clean" {
    commands     = ["init", "apply", "destroy"]
    execute      = ["rm", "-rfv", "modules"]
    run_on_error = true

  }

}

# main.tf

module "m1" {
  source = "./modules/m1"
}

```

https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#terraform
***

**NicolaSpreafico** commented *Jan 27, 2023*

Thank you, this made my day, works very well (I added the `plan` as well)
```
locals {
   terraform_directory = "${get_terragrunt_dir()}/../../../modules/${local.module}"
}

terraform {
  # (double slash ending to avoid "No double-slash (//) found in source URL")
  source = "${local.terraform_directory}///"

  # Copy shared modules
  before_hook "before" {
    commands     = ["init", "apply", "destroy", "plan"]
    execute      = ["cp", "-rfv", "${local.terraform_directory}/../_shared_modules", "."]
  }

  after_hook "clean" {
    commands     = ["init", "apply", "destroy", "plan"]
    execute      = ["rm", "-rfv", "_shared_modules"]
    run_on_error = true
  }
}
```

Only  a question remains, is there a way to remove from the logs the commands output?
***

**denis256** commented *Jan 27, 2023*

Hi,
try to remove `"v"` from parameters, it should reduce output volume
***

**NicolaSpreafico** commented *Jan 27, 2023*

Thank you, much better.


***

