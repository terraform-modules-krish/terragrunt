# Need some help on how to use dependencies in Terragrunt in order to pass IP addresses when creating a Load Balancer

**gibsster** commented *Aug 1, 2020*

Hi Guys,

I haven't been able to make dependency work in terragrunt.

I have a module called DTS that creates an instance and then a load balancer in Oracle Cloud. Somehow I need to pass the IP address of the instance when the load balancer gets created.

DTS module is in a different git repository from my terragrunt project

my terragrunt project looks like this.

```
dts
├── data
│   ├── data.tf
│   └── terragrunt.hcl
└── terragrunt.hcl
```

in the `dts/data/data.tf` I have this:

```
data "oci_core_instances" "this" {
  compartment_id = var.default_compartment_id
  filter {
    name = "display_name"
    values = var.instance_names
  }
}

data "oci_core_vnic_attachments" "this" {
  compartment_id = var.default_compartment_id
  count       = length(data.oci_core_instances.this.instances)
  instance_id = data.oci_core_instances.this.instances[count.index].id
}

data "oci_core_vnic" "this" {
  count       = length(data.oci_core_instances.this.instances)
  vnic_id = lookup(flatten(data.oci_core_vnic_attachments.this.*.vnic_attachments)[count.index], "vnic_id")
  #vnic_id = element([ for x in flatten(data.oci_core_vnic_attachments.vinc_ids.*.vnic_attachments): x.vnic_id ], count.index)
}

variable "default_compartment_id" {}
variable "instance_names" {
  type = list(string)
}

output "instance_ips"{
  value = [
    for x in data.oci_core_vnic.this:
      x.private_ip_address
    ]
}
```

There I'm outputting the instance IPs based on instance names provided by the user.

in the `dts/data/terragrunt.hcl` I have the following:

```
terraform {
  source = "./"
}

locals {
  default_compartment_id = "ocid1.compartment.oc1..aaaaaaaai6v35ydiz6harzbovom6rua56isfxz2epxgeqthjxo3vz5ljikqq"
  instance_names = ["oasp-3-dts-1"]
}
```

and in the `dts/terragrunt.hcl` I have:

```
include {
  path = find_in_parent_folders()
}

terraform {
  source = "git::ssh://alm.oraclecorp.com:2222/acs_terraform_26775/dts.git?ref=terragrunt_implementation"
}

dependency "data" {
  config_path = "./data"
}

inputs = {
  default_compartment_id = "ocid1.compartment.oc1..aaaaaaaai6v35ydiz6harzbovom6rua56isfxz2epxgeqthjxo3vz5ljikqq"
  
  certs = ["oasp-3/dts/ca.crt", "oasp-3/dts/cert.key", "oasp-3/dts/cert.crt"]
  
  instances = {
      oasp-3-dts-1 = {
        ad             = 0                      #0-AD1, 1-AD2, 2-AD3 RequiredRequired
        compartment_id = null #Required
        shape          = "VM.Standard2.1"           #Required

        subnet_id      = "ocid1.subnet.oc1.iad.aaaaaaaahdna2dxmc7n73bl6pg7jg74um6zvcqbem22mqrqcsugscvbjve3a" #"ocid1.xxxxx"

        is_monitoring_disabled = null

        assign_public_ip    = false
        vnic_defined_tags   = null
        vnic_display_name   = null
        vnic_freeform_tags  = null
        nsg_ids             = null #["ocid1.xxxxx"]
        private_ip          = null
        skip_src_dest_check = null

        defined_tags          = null
        extended_metadata     = null
        fault_domain          = null
        freeform_tags         = {"DeployedBy": "terraform"}
        hostname_label        = null
        ipxe_script           = null
        pv_encr_trans_enabled = null

        ssh_authorized_keys = ["ssh_key.pub"]  #ex: ["/path/public-key.pub"]
        ssh_private_keys    = null #ex: ["/path/private-key"]
        bastion_ip          = null
        user_data           = null #base64encode(file("bootstrap.sh"))

        // See https://docs.cloud.oracle.com/iaas/images/
        image_name             = "Oracle-Linux-7.8-2020.06.30-0" #Required
        source_id              = null                            #"ocid1.image.oc1.eu-frankfurt-1.aaaaaaaax3xjmpwufw6tucuoyuenletg74sdsj5f2gzsvlv4mqbbgeokqzsq" #"ocid1.image.oc1.iad.aaaaaaaay66pu7z27ltbx2uuatzgfywzixbp34wx7xoze52pk33psz47vlfa"  #Required
        mkp_image_name         = null
        mkp_image_name_version = null
        source_type            = null
        boot_vol_size_gbs      = 50
        kms_key_id             = null

        preserve_boot_volume = null
        instance_timeout     = null
        sec_vnics            = null #{} #
        block_volumes        = null
      },
    }


  lb_options = {
    display_name          = "oasp3_lb1"
    compartment_id        = null
    shape                 = "100Mbps"
    subnet_ids            = ["ocid1.subnet.oc1.iad.aaaaaaaahdna2dxmc7n73bl6pg7jg74um6zvcqbem22mqrqcsugscvbjve3a"]
    private               = true
    nsg_ids               = null
    defined_tags          = null
    freeform_tags         = {"DeployedBy": "terraform"}
  }

  backend_sets            = {
    oasp3_lb1_bes1        = {
      policy              = "ROUND_ROBIN"
      health_check_name   = "basic_http"
      enable_persistency  = false
      enable_ssl          = false

      cookie_name         = null
      disable_fallback    = null
      certificate_name    = null
      verify_depth        = null
      verify_peer_certificate = null

      backends            = {
        be1               = {
          ip              = dependency.data.outputs.instance_ips[0] //"100.100.180.43"
          port            = 8080
          backup          = false
          drain           = false
          offline         = false
          weight          = 1
        },
      }
    }
  }

  health_checks           = {
    basic_http            = {
      protocol            = "HTTP"
      interval_ms         = 120000
      port                = 8080
      response_body_regex = ".*"
      retries             = 3
      return_code         = 200
      timeout_in_millis   = 10000
      url_path            = "/"
    }
  }

  listeners               = {
    oasp3_lb1_lsnr_https  = {
      default_backend_set_name = "oasp3_lb1_bes1"
      port                = 443
      protocol            = "HTTP"
      idle_timeout        = 60
      hostnames           = null
      path_route_set_name = null
      rule_set_names      = null
      enable_ssl          = true
      certificate_name    = "oasp3_lb1_cert1"
      verify_depth        = 5
      verify_peer_certificate = true
    }
  }
}
```


In the `backends` block is where I need to pass the IP `ip = dependency.data.outputs.instance_ips[0]` so as you can see I'm declaring the below  dependency on `dts/data/data.tf` but when I run `terragrunt apply` It complains about not detecting outputs.

```
dependency "data" {
  config_path = "./data"
}
```
 
This is the error: 
**/oracle_git_repos/oasp/oasp-3/dts/data/terragrunt.hcl is a dependency of /oracle_git_repos/oasp/oasp-3/dts/terragrunt.hcl but detected no outputs. Either the target module has not been applied yet, or the module has no outputs. If this is expected, set the skip_outputs flag to true on the dependency block.**

Does anybody have any idea what I'm doing wrong?. Unfortunately, I haven't been able to find good example about terragrunt dependencies.

Thanks in advance!
<br />
***


**yorinasub17** commented *Aug 1, 2020*

You need to run `apply` in the `data` module first, even if it is just data sources.

This is because of a limitation of how terraform/terragrunt works. `dependency` blocks under the hood call `terragrunt output` on the target module, which in turn calls `terraform output`. Under the hood, `terraform output` pulls down the state file to get the output data. So for this to work, the state file needs to be updated with the outputs, and the only way to do that is to run `apply`, even if you have no resources.
***

