# Templating tfvars file?

**dudicoco** commented *Feb 18, 2020*

Is it possible to template tfvars files using variables loaded via the ```inputs``` or ```include``` blocks?

Example tfvars file:
```
region = var.region

vpc_id = var.vpc_id
```
<br />
***


**yorinasub17** commented *Feb 18, 2020*

`tfvars` files are not processed by terraform so you won't be able to use variables in them. However, you can use the new `generate` block to template out a `tfvars` file in terragrunt where you can reference anything that is available to the `terragrunt` context such as `locals` and `dependency`.

With that said, what is the exact use case you are trying to achieve here? That is, since the `inputs` are already being piped as variable inputs, you shouldn't need a tfvars file which provides further input variable values.
***

**dudicoco** commented *Feb 18, 2020*

Thanks as always for the fast response @yorinasub17 :)

My use case is when defining IAM policies in my var file, I sometimes need to reference other resources and I prefer not to hardcode them in the file.

I am actually not really using a tfvars file but a yaml file which I load via the yamldecode function within the terragrunt.hcl inputs block.

example yaml file:
```
iamRole:
  policyStatements:
  - Effect: Allow
    Action:
    - sts:AssumeRole
    Resource:
    - arn:aws:iam::1234567890:role/my-role
  - Effect: Allow
    Action:
    - route53:ChangeResourceRecordSets
    Resource:
    - arn:aws:route53:::hostedzone/ABCDEFG1234567890
```
***

**yorinasub17** commented *Feb 18, 2020*

For this I would recommend using `dependency` blocks, which allow you to reference and use outputs of other modules, and also avoid using YAML. For example, if you had a module that managed the Route 53 hosted zone, you could do:

```hcl
dependency "route53" {
  config_path = "${get_terragrunt_dir()}/../route53"
}

inputs = {
  policy_statements = [{
    Effect = "Allow"
    Action = ["route53:ChangeResourceRecordSets"]
    Resource = [dependency.route53.outputs.hosted_zone_arn]
  }]
}
```

If you wanted to make this reusable, you can use the new `read_terragrunt_function` we just released. E.g if you had the above as a config living somewhere under the name `route53_policy.hcl`, you can read that into another config to reuse:

```hcl
locals {
  route53_hosted_zone_access_policy = read_terragrunt_config("${get_terragrunt_dir()}/../route53_policy.hcl")
}

inputs = {
  policy_statements = local.route53_hosted_zone_access_policy.inputs.policy_statements
}
```
***

**dudicoco** commented *Feb 18, 2020*

@yorinasub17 Why should yaml files be avoided?.

I do use a dependency block but I don't want to place the policies within the inputs block, also as you can see in the example yaml file the policy statements depends on two different resource (and in some cases more).





***

**yorinasub17** commented *Feb 18, 2020*

YAML is a static configuration format so you can't do any kind of interpolation or processing on them. You want something that does data processing on top of configuration, which means you need something else. In the example above, especially with `read_terragrunt_config`, I am using the terragrunt configuration format to implement data processing over the configuration.

Can you share what the ideal interface that you desire is? I think what you want exists, but without knowing exactly what you are looking for, it is a bit hard to help out with suggestions.
***

**dudicoco** commented *Feb 19, 2020*

So how my main terragrunt.hcl file is configured to load a global yaml var file and a specific environment yaml var file according to an environment variable:
```
locals {
  default_yaml_path     = "${get_parent_terragrunt_dir()}/empty.yaml"
  yaml_path             = "${get_terragrunt_dir()}/values_${get_env("TF_VAR_environment", "")}.yaml"
  environment_yaml_file = fileexists(local.yaml_path) ? local.yaml_path : local.default_yaml_path
}

inputs = merge(
  yamldecode(
    file("${get_parent_terragrunt_dir()}/global/global-values_${get_env("TF_VAR_environment", "")}.yaml")
  ),
  yamldecode(
    file(local.environment_yaml_file),
  ),
  {
    team = "${split("/", path_relative_to_include()).0}"
  },
  {
    account = "${split("_", get_env("TF_VAR_environment", "")).0}"
  }
)
```

I prefer to have all of my variables declared within a yaml file for each environment as it's more human readable and it's easier to understand what your infrastructure looks like if it's described in this yaml file and not in additional files.
So if I have an IAM module, I want to define the different policies for each IAM role in a yaml file, so if I have 200 IAM roles, it's easy to understand policies for each one just by looking at the yaml file and without checking additional files.

***

**dudicoco** commented *Feb 19, 2020*

Is it possible perhaps to use anchors and aliases in the yaml file and get their values as input via terragrunt?
***

**dudicoco** commented *Feb 19, 2020*

Looks like anchors and aliases won't work since the anchor must come from outside the yaml file.

I've found a partial solution by adding the following to my main ```terragrunt.hcl```:
```
inputs = merge(
  yamldecode(
    file("${get_parent_terragrunt_dir()}/global/global-values_${get_env("TF_VAR_environment", "")}.yaml")
  ),
  yamldecode(
    replace(file(local.environment_yaml_file), "hostedZone", "arn:aws:route53:::hostedzone/ABCDEFG1234567890"),
  ),
  {
    team = "${split("/", path_relative_to_include()).0}"
  },
  {
    account = "${split("_", get_env("TF_VAR_environment", "")).0}"
  }
)
```

Yaml file:
```
iamRole:
  policyStatements:
  - Effect: Allow
    Action:
    - route53:ChangeResourceRecordSets
    Resource:
    - hostedZone
```

I just need to find a way to move part of the logic to the child ```terragrunt.hcl``` file and have the parent file code generic, so maybe replace all strings with some kind of prefix and suffix, such as ```{myvar}```.  So i'm trying to find a character that won't disturb the yaml code or the terraform code.

Any ideas?

Thanks!
***

**yorinasub17** commented *Feb 19, 2020*

I think what you want is https://www.terraform.io/docs/configuration/functions/templatefile.html
***

**dudicoco** commented *Feb 19, 2020*

Thanks, I'll look into it and report back.
***

**dudicoco** commented *Feb 20, 2020*

So I got it to work, this is the content of the child ```terragrunt.hcl```:
```
terraform {
  source = "my_module"
}

skip = fileexists("${get_env("TF_VAR_environment", "")}.yaml") ? false : true

dependency "hosted-zone" {
  config_path  = "../hosted-zone"

  mock_outputs = {
    hosted_zone = ["known after apply"]
  }
}

dependency "vpc" {
  config_path  = "../vpc"

  mock_outputs = {
    vpc_id = ["known after apply"]
  }
}

locals {
  default_yaml_path        = "${get_parent_terragrunt_dir()}/empty.yaml"
  yaml_path                     = "${get_terragrunt_dir()}/${get_env("TF_VAR_environment", "")}.yaml"
  environment_yaml_file = fileexists(local.yaml_path) ? local.yaml_path : local.default_yaml_path
}

inputs = merge(
  yamldecode(
    templatefile(
      local.environment_yaml_file,
      {
        # Variables defined here will be available within the environment YAML file as ${var_name}
        hosted_zone = dependency.hosted-zone.outputs.hosted_zone.0
      }
    )
  ),
  {
    # Variables defined here will be available within the module
    vpc_id = dependency.vpc.outputs.vpc_id.0
  }
)


include {
  path = find_in_parent_folders()
}
```

YAML file:
```
iamRole:
  policyStatements:
  - Effect: Allow
    Action:
    - route53:ChangeResourceRecordSets
    Resource:
    - ${hosted_zone}
  - Effect: Allow
    Action:
    - route53:ListHostedZones
    - route53:ListResourceRecordSets
    Resource:
    - "*"
```

This is working great with no hardcoding within the var file.
Is there a way to move some of this logic back to the parent ```terragrunt.hcl```?
***

**yorinasub17** commented *Feb 20, 2020*

Great! Going to close this out.

> Is there a way to move some of this logic back to the parent terragrunt.hcl?

https://github.com/gruntwork-io/terragrunt/pull/1025 is probably going to give you the tools necessary to refactor your config.
***

