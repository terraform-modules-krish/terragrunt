# terragunt always runs terraform init -input=false

**aavileli** commented *Apr 22, 2023*

After successfully running **terragrunt init** and then executing **terragrunt run-all plan --terragrunt-non-interactive  --terragrunt-no-auto-init**, terragrunt always issues **terragrunt init -input=false** in the background. Discovered this using process/thread inspection as the terragrunt debug log did not print anything and would just hang, it would later start excuting the plan after 75s.

tracing the process with strace revealed it is requesting packets to S3 remote state.
I then added this option to the S3 remote_state  **disable_dependency_optimization = true** which disables the always running **terraform init** . 

Setup :-
- Terragrunt 0.45.4
- Terraform 1.4.2
- Execution in Podman 4.5.0 fedora 37 container
- Provider versions
└── registry.terraform.io
    └── hashicorp
        ├── archive
        │   └── 2.3.0
        ├── aws
        │   └── 4.64.0
        ├── cloudinit
        │   └── 2.3.2
        ├── external
        │   └── 2.3.1
        ├── helm
        │   └── 2.9.0
        ├── kubernetes
        │   └── 2.20.0
        ├── local
        │   └── 2.4.0
        ├── null
        │   └── 3.2.1
        ├── random
        │   └── 3.5.1
        ├── time
        │   └── 0.9.1
        └── tls
            └── 4.0.4

Is this a bug, is there anything I can try to find out why this behaviour occurs.

Adding the root terragrunt.hcl definition reference 

```hcl
locals {
  terraform_state_s3_bucket        = "org1-terraform"
  terraform_state_s3_bucket_region = local.global_vars["region"]
  terraform_config_profile         = local.global_vars["aws_profile"]
  terraform_state_dynamodb_table   = "org1-terraform-lock"
  terraform_state_s3_key           = "terragrunt/${path_relative_to_include()}/terraform.tfstate"
  global_vars                      = yamldecode(file(find_in_parent_folders("global_settings.yaml")))
  terraform_cache_dir              = format("%s/%s", get_env("TF_PLUGIN_CACHE_DIR",  abspath("${get_repo_root()}/.terragrunt-cache/.plugins")), path_relative_to_include())
}

remote_state {
  backend = "s3"
  config = {
      dynamodb_table = local.terraform_state_dynamodb_table
      bucket         = local.terraform_state_s3_bucket
      region         = local.terraform_state_s3_bucket_region
      profile        = local.terraform_config_profile
      key            = local.terraform_state_s3_key
      encrypt        = true

      s3_bucket_tags = {
        Name = "Terraform state storage"
      }

      dynamodb_table_tags = {
        Name = "Terraform lock table"
      }
  }
}

terraform {
  extra_arguments "disable_input" {
    commands  = get_terraform_commands_that_need_input()
    arguments = ["-input=false"]
  }


  before_hook "copy_global_providers" {
    commands     = ["init-from-module","plan"]
    execute      = ["wget", "-qN", "https://raw.githubusercontent.com/org1/terraform-modules-public/v0.0.6/_global/_global_providers.tf"]
    run_on_error = true
  }

  
  before_hook "provider_cache" {
    commands = ["init", "validate", "plan", "apply"]
    execute  = ["mkdir", "-pv", local.terraform_cache_dir]
  }

  extra_arguments "regional_vars" {
    commands = get_terraform_commands_that_need_vars()

    optional_var_files = [
      "${get_parent_terragrunt_dir()}/_global/global.tfvars",
      "${get_terragrunt_dir()}/../regional.tfvars",
      "${get_terragrunt_dir()}/../../regional.tfvars",
      "${get_terragrunt_dir()}/../../../regional.tfvars",
      "${get_terragrunt_dir()}/../../../../regional.tfvars",
      "${get_terragrunt_dir()}/../terragrunt.tfvars",
      "${get_terragrunt_dir()}/../../terragrunt.tfvars",
      "${get_terragrunt_dir()}/../../../terragrunt.tfvars",
      "${get_terragrunt_dir()}/../../../../terragrunt.tfvars",
    ]
  }

// adding caching of plugins instead of downloading again /tmp/tfplugins
  extra_arguments "set_env_vars" {
    commands = [get_terraform_command()]

    env_vars = {
      TF_PLUGIN_CACHE_DIR = local.terraform_cache_dir
      KUBE_CONFIG_PATH = get_env("KUBE_CONFIG_PATH", "~/.kube/config")
    }
  }

}

download_dir = abspath("${get_repo_root()}/.terragrunt-cache")

# Configure root level variables that all resources can inherit. This is especially helpful with multi-account configs
# where terraform_remote_state data sources are placed directly into the modules.
inputs = {
  aws_profile                      = local.terraform_config_profile
  aws_allowed_account_ids          = tolist([local.global_vars["aws_account_id"]])  # we should use list(get_aws_account_id()), but this does not work yet: https://github.com/gruntwork-io/terragrunt/issues/791
  terraform_state_s3_bucket        = local.terraform_state_s3_bucket
  terraform_state_s3_bucket_region = local.terraform_state_s3_bucket_region
  terraform_state_dynamodb_table   = local.terraform_state_dynamodb_table
}
```


 
<br />
***


**denis256** commented *Apr 23, 2023*

Hello,
can be shared example repository where this issue happens?
I tried to setup something similar in https://github.com/denis256/terragrunt-tests/tree/master/issue-2543 but in my case, I don't see to appear in processes invocations of `terraform init -input=false`
***

**aavileli** commented *Apr 24, 2023*

Hi 
Cant share the repo as its private to the organisation.
This is not happening in your test case you have shared. I believe the change started happening from terraform 1.4.0 and I think it's related to this change https://github.com/hashicorp/terraform/pull/32129. 

I will attempt to further debug the issue on why dependency modules are running **terraform init -input=false** when getting dependency output. There was change done in the 1.4.x for init in terraform. 

The behavior is like it pauses ` Getting output of dependency  `  if I issue a **ctrl-c** (SIGINT) when that happens it then continuous fine. I am now using **--terragrunt-fetch-dependency-output-from-state** as a workaround to speedup applying changes.



***

**thisispaulsmith** commented *Apr 24, 2023*

Possibly related to #2519?
***

**aavileli** commented *Apr 28, 2023*

After performing the following changes the issue does not affect me anymore

- disabled the use of ipv6 dns response for hasicorp registry dns (when using the ipv6 ip the requests where slower on systemd-resolved hosts)
- replaced the following 
```hcl
terraform_cache_dir = format("%s/%s", get_env("TF_PLUGIN_CACHE_DIR",  abspath("${get_repo_root()}/.terragrunt-cache/.plugins")), path_relative_to_include())
```
with
```hcl
terraform_cache_dir = format("%s/%s", get_env("TF_PLUGIN_CACHE_DIR", "/tmp/.tf-plugin-cache"), path_relative_to_include())
```
- cleared all terragrunt cache and lock files
- executed **terragrunt run-all init** 
- executed **terragrunt run-all plan  --terragrunt-non-interactive --terragrunt-include-module-prefix**

It does not run init anymore, but if I clear the cache directory it will take 5m as it has to download the providers for each module. Due to the [race condition](https://github.com/hashicorp/terraform/pull/32928) on the shared plugin cache,  **_path_relative_to_include()_** function  has to be used so that plugins are downloaded for each module . Hope that gets fixed sometime.

Also just using this flag to generate the dependency output  is always quicker  **--terragrunt-fetch-dependency-output-from-state**
so learnt a new flag.

***

