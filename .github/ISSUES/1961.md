# How to include a reference for default_tags in aws provider 

**jebcolesd** commented *Dec 21, 2021*

I'd like the aws provider generator to use locals when creating the default_tags property. Unfortunately this seems to fail. 

`terragrunt.hcl`
locals {
aws_region = "us-east-1"
aws_profile = "prod"
aws_tags = { test = "test" }
}

generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
provider "aws" {
  region = "${local.aws_region}"
  profile = "${local.aws_profile}"
  default_tags {
    tags = local.aws_tags
  }
}
EOF
}

including this configuration will fail saying that local.tags does not exist. There is no problem finding the other locals. Putting {test = "test" }  directly into the default_tags succeeds, but I want these to be generated dynamically.
<br />
***


**denis256** commented *Dec 21, 2021*

Hi, not sure if it is the best approach, but I think complex variables can be passed through `inputs` block

```
locals {
    aws_region = "us-east-1"
    aws_profile = "prod"
    aws_tags = { test = "test" }
}


inputs = {
    aws_tags = local.aws_tags
}

generate "provider" {
path = "provider.tf"
if_exists = "overwrite_terragrunt"
contents = <<EOF

variable "aws_tags" {
   type = map
}

provider "aws" {
    region = "${local.aws_region}"
    profile = "${local.aws_profile}"
    default_tags {
        tags = var.aws_tags
    }
}

EOF
}

```
***

**yorinasub17** commented *Jan 13, 2022*

In addition to the approach @denis256 posted, you can also use json as an intermediary:

```hcl

locals {
  aws_region = "us-east-1"
  aws_profile = "prod"
  aws_tags_json = jsonencode({ test = "test" })
}

generate "provider" {
path = "provider.tf"
if_exists = "overwrite_terragrunt"
contents = <<EOF
provider "aws" {
  region = "${local.aws_region}"
  profile = "${local.aws_profile}"
  default_tags {
# Use heredoc syntax to render the json to avoid quoting complications.
    tags = jsondecode(<<INNEREOF
${local.aws_tags}
INNEREOF
)
  }
}
EOF
}
```
***

**yorinasub17** commented *Jan 13, 2022*

Closing as answered.
***

