# [question] Getting child terragrunt.hcl file location from read_terragrunt_config(find_in_parent_folders(xxx))

**nickrichardson-presto** commented *Jun 15, 2021*

I have some common logic I include at some higher level than the child terragrunt.hcl file. I want to somehow get the location of that child terragrunt.hcl but I can't seem to. Here is my directory structure:

```
.
â”œâ”€â”€ terragrunt.hcl
â””â”€â”€ non-prod/
    â”œâ”€â”€ account.hcl
    â”œâ”€â”€ versions.hcl
    â”œâ”€â”€ regional-infra/
    â”‚   â””â”€â”€ us-west-2/
    â”‚       â”œâ”€â”€ region.hcl
    â”‚       â”œâ”€â”€ eks-cluster/
    â”‚       â”‚   â””â”€â”€ terragrunt.hcl
    â”‚       â””â”€â”€ vpc/
    â”‚           â””â”€â”€ terragrunt.hcl
    â””â”€â”€ environments/
        â””â”€â”€ us-west-2/
            â””â”€â”€ staging/
                â””â”€â”€ my-app/
                    â””â”€â”€ terragrunt.hcl
```
where `versions.hcl` looks like this:

```
locals {
  git_repo            = "git::git@<my-repo>/infrastructure-terraform-modules.git"
  environmental-infra = {
    us-west-2 = {
      dev = {
        my-app = "v0.4.3"
      }
    }
  }
  regional-infra      = {
    us-west-2 = {
      vpc         = "v0.4.3"
      eks-cluster = "v0.4.3"
    }
  }
}
```

I'm able to pull this information inside my child terragrunt.hcl file like this

```
locals {
  account_vars = read_terragrunt_config(find_in_parent_folders("account.hcl"))
  account_name = local.account_vars.locals.account_name
  parsed_path  = regex(".*/${local.account_name}/(?P<type>.*?)/(?P<region>.*?)/(?P<module>.*)", abspath(path_relative_to_include()))
  type         = local.parsed_path.type
  region       = local.parsed_path.region
  module       = local.parsed_path.module

  module_vars    = read_terragrunt_config(find_in_parent_folders("module-versions.hcl"))
  module_version = local.module_vars.locals[local.type][local.region][local.module]
  git_repo       = local.module_vars.locals["git_repo"]
}

terraform {
  source = "${local.git_repo}//<<module name>>?ref=${local.module_version}"
}
```

Now all of my modules have this information which is quite verbose and not DRY.
I tried pulling most of that into something called `config.hcl` but the regex is failing because of the parsed path not matching.

```
abspath(path_relative_to_include())
```
I need to replace this function call to become something like `/non-prod/regional-infra/us-west-2/vpc/` but nothing I've tried has worked!

Is this possible with my current setup?
<br />
***


**cognitiaclaeves** commented *Jun 17, 2021*

@nickrichardson-presto , it kind of hurts my brain to try to parse all that out. But I think I might be doing that kind of thing here: https://gitlab.com/cognitiaclaeves/terragrunt-infrastructure-live-example/-/tree/tfswitch-json/tg-alt/acct1/us-east-1/dev/quick-and-dirty-mock-vpc (note: needs to be that branch and starting at /tg-alt/*), also need to set this environment variable, to make it work: `export TERRAGRUNT_CONFIG=tg-alt.hcl` 
***

**brikis98** commented *Jun 17, 2021*

See [`get_parent_terragrunt_dir()`](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#get_parent_terragrunt_dir) and [`get_original_terragrunt_dir()`](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#get_original_terragrunt_dir).
***

**yorinasub17** commented *Jun 17, 2021*

One note of clarification: `read_terragrunt_config` does not support the `*parent*` and `*original*` and `*relative_(to|from)_include` functions. All those functions are intended for use with `include`, not with `read_terragrunt_config`.
***

**stevie-** commented *Jun 18, 2021*

If I got you right, you add in each child the code to get the module path from directory path and you already see this is not DRY.

We also do this, but we put this in the parent `terragrunt.hcl`. Since we use this to share common settings to all childs. The child `terragrunt.hcl` usually can overwrite most of these common settings if needed.

So I suggest you to move the following snippet in your **parent** `terragrunt.hcl`.
I tried to adapt the `account_vars`/`module_vars` logic to your current situation, untested code)

```hcl
locals {
  # assumption: `path_relative_to_include()` should look like this `on-prod/regional-infra/us-west-2/eks-cluster`
  parsed_path   = regex("(?P<account_type>.*?)/(?P<type>.*?)/(?P<region>.*?)/(?P<module>.*)", path_relative_to_include()) 
  account_type  = local.parsed_path.account_type  # need dir to build path from parent dir
  type          = local.parsed_path.type
  region        = local.parsed_path.region
  module        = local.parsed_path.module

  account_vars   = read_terragrunt_config("${local.account_type}/account.hcl") # path from parent_dir
  account_name   = local.account_vars.locals.account_name
  module_vars    = read_terragrunt_config("${local.account_type}/versions.hcl") # path from parent_dir
  module_version = local.module_vars.locals[local.type][local.region][local.module]
  git_repo       = local.module_vars.locals["git_repo"]
}

terraform {
  source = "${local.git_repo}//${local.module}?ref=${local.module_version}"
}
```

***

**nickrichardson-presto** commented *Jun 23, 2021*

Thanks @stevie- ! I'm not sure why I didn't think to just move that code to the parent terragrunt.hcl ðŸ¤¦â€â™‚ï¸
***

**stevie-** commented *Jun 24, 2021*

> One note of clarification: `read_terragrunt_config` does not support the `*parent*` and `*original*` and `*relative_(to|from)_include` functions. All those functions are intended for use with `include`, not with `read_terragrunt_config`.

This should be mentioned in docs? @yorinasub17 
***

