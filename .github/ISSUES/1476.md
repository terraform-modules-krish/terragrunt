# General Guidance Around Merging Maps/Tags 

**tarpanpathak** commented *Dec 30, 2020*

**Terraform version**: 0.12.20
**Terragrunt version**: 0.21.11

As a test, we are using a project with the following directory structure:
```
./
└── terraform
    ├── aws
    │   ├── <account>
    │   │   ├── account.tfvars
    │   │   └── us-west-2
    │   │       ├── <env_suffx>-myapp-s3-bucket
    │   │       │   ├── terraform_init.tf
    │   │       │   └── terragrunt.hcl
    │   │       ├── <env_suffix>-myapp-s3-bucket
    │   │       │   ├── terraform_init.tf
    │   │       │   └── terragrunt.hcl
    │   │       ├── region.tfvars
    │   │       └── <env_suffix>-myapp-s3-bucket
    │   │           ├── terraform_init.tf
    │   │           └── terragrunt.hcl
    │   └── <account_2>
    │       ├── account.tfvars
    │       └── us-west-2
    │           ├── <env_suffix>-myapp-s3-bucket
    │           │   ├── terraform_init.tf
    │           │   └── terragrunt.hcl
    │           └── region.tfvars
    ├── modules
    │   └── s3
    │       ├── README.md
    │       ├── main.tf
    │       ├── outputs.tf
    │       ├── variables.tf
    │       └── versions.tf
    └── terragrunt.hcl
```
For consistency sake, the module uses the following naming convention for each bucket:
```sh
name = "${var.default_tags["environment"]}-${var.default_tags["env_suffix"]}-${var.default_tags["application_name"]}-${var.default_tags["component"]}"
```
`default_tags` is defined like so:
```sh
.....

variable "default_tags" {
  description = "(Optional) A mapping of tags to assign to the bucket."
  type        = map(string)
  default     = {}
}

.....
```
FYI, a majority of the tags are shared across all the resources in the account and so, they can be defined at the account-level. For this reason, the `terraform/aws/<account>/account.tfvars` looks like so:
```sh
default_tags = {
  application_name  = "<application_name>"
  environment       = "<environment>"
  .....
}
```
Some `tags` however, must be component/environment-specific. For this reason, the `terraform/aws/<account>/us-west-2/<env_suffix>-myapp-s3-bucket/terragrunt.hcl` looks like so:
```sh
.....

inputs = {
    default_tags = {
      component     = "<component>"
      env_suffix    = "<env_suffix>"
    }
}
```
Running a `terragrunt plan` on the structure outlined above, returns the following error:
```sh
Error: Invalid index

  on main.tf line 2, in locals:
   2:   name = "${var.default_tags["environment"]}-${var.default_tags["env_suffix"]}-${var.default_tags["application_name"]}-${var.default_tags["component"]}"
    |----------------
    | var.default_tags is map of string with 15 elements

The given key does not identify an element in this collection value.


Error: Invalid index

  on main.tf line 2, in locals:
   2:   name = "${var.default_tags["environment"]}-${var.default_tags["env_suffix"]}-${var.default_tags["application_name"]}-${var.default_tags["component"]}"
    |----------------
    | var.default_tags is map of string with 15 elements

The given key does not identify an element in this collection value.
```
Based on the version of Terraform/Terragrunt being used, after reviewing a few of the following issues/examples:
- https://github.com/gruntwork-io/terragrunt/issues/744
- https://github.com/gruntwork-io/terragrunt/issues/1011
- https://github.com/gruntwork-io/terragrunt-infrastructure-modules-example
- https://github.com/gruntwork-io/terragrunt-infrastructure-live-example
- https://www.terraform.io/docs/configuration/functions/merge.html

I am a bit confused on the best approach for merging the required `tags`. Would someone be able to point me in the right direction for addressing our use-case? I'm happy to provide more details if required.
<br />
***


**yorinasub17** commented *Jan 4, 2021*

Terragrunt currently does not deep merge the inputs, so you will need to explicitly merge them together using `merge` function. The best way to do this would be to use `read_terragrunt_config` to load these common vars into the `terragrunt.hcl` file as opposed to passing them through to terraform with `-var-file` and `extra_arguments`.

For example, you can define a file `terraform/aws/<account>/account_tags.hcl` that defines:

```hcl
locals {
  default_tags = {
    application_name  = "<application_name>"
    environment       = "<environment>"
    .....
  }
}
```

And then in your child config, do the following:

```hcl
locals {
  account_tags = read_terragrunt_config(find_in_parent_folders("account_tags.hcl"))
  env_tags = {
      component     = "<component>"
      env_suffix    = "<env_suffix>"
  }
}

inputs = {
    default_tags = merge(local.account_tags.locals.default_tags, local.env_tags)
}
```
***

**tarpanpathak** commented *Jan 4, 2021*

Thx @yorinasub17 . I have just tested the approach you've suggested and it works fine. Note, I did have to bump Terragrunt to `v0.23.0` so `read_terragrunt_config` is available. I do have a follow-up question for you: 

1. I think the answer at this time is no but just to confirm, in order to follow DRY, is it possible to define the following in one place, e.g. in the root `terragrunt.hcl` config: 
```
locals {
  account_tags = read_terragrunt_config(<some_built_in_function>("account_tags.hcl"))
}
```
And importing/performing a merge in the child using something like so: 
```
locals {
  env_tags = {
      component     = "<component>"
      env_suffix    = "<env_suffix>"
  }
}

inputs = {
    default_tags = merge(<imported_map_of_account_tags>, local.env_tags)
}
```
***

**yorinasub17** commented *Jan 5, 2021*

Unfortunately, this is currently not possible. In the future we plan to have a construct that allows this, but we haven't had the chance to implement it yet.
***

**tarpanpathak** commented *Jan 5, 2021*

Thx again. 
***

