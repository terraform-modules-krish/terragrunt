# [Question] - generate multiple providers with for_each key

**kamigerami** commented *Nov 25, 2020*

Is it possible to generate a provider configuration using a list/set and for_each key.

obviously, the example below does not work. 

Maybe tg has / can add an builtin for_each function or some other clever solution


I know you can generate a basic required_providers versions.tf file by running terraform / terragrunt 0.13upgrade however what I want to do is add versions by reading from a locals in my terragrunt.hcl

Example pseudo code below:

```
locals {
  providers = read_terragrunt_config(find_in_parent_folders("providers.hcl"))
}
```
containing stuff like:

```
locals {
  google = "3.48.0"
  google-beta = "3.48.0"
  helm = "4.5.6"
  random = "1.0.0"
}
```

```
generate "provider_versions" {
  path      = "versions.tf"
  if_exists = "overwrite"
  contents  = <<EOF
  for_each = toset( [ "google", "google-beta", "helm", "random"] )

terraform {
  required_providers {
    ${each.key} = {
      source = "hashicorp/${each.key}"
      version = "${local.providers.locals.${each.key}}"
    }
  }
  required_version = ">= 0.13"
}

EOF
}
```

The expected output file would be something like this: versions.tf

```  
terraform {
  required_providers {
    google = {
      source = "hashicorp/google"
      version = "3.48.0"
    }
    google-beta = {
      source = "hashicorp/google-beta"
      version = "3.48.0"
    }
    helm = {
      source = "hashicorp/helm"
      version = "4.5.6"
    }
    random = {
      source = "hashicorp/random"
      version = "1.0.0"
    }
 }
  required_version = ">= 0.13"
}
```
Any alternative clever solutions are welcome :) 
<br />
***


**brikis98** commented *Nov 30, 2020*

Yup, you can use a [for directive!](https://www.terraform.io/docs/configuration/expressions.html#directives)

For example, we had one use case where we wanted to have a `provider` block for each child account. In `terragrunt.hcl`, we did the following:

```hcl
# Define the child accounts
locals {
  child_accounts = {
    security = {
      email = "root-accounts+security@acme.com",
    },
    shared-services = {
      email = "root-accounts+shared-services@acme.com"
    },
    dev = {
      email = "root-accounts+dev@acme.com"
    },
    stage = {
      email = "root-accounts+stage@acme.com"
    },
    prod = {
      email = "root-accounts+prod@acme.com"
    }
  }

  aws_region = "us-east-1"
}

# Generate a provider for each child account
generate "providers" {
  path      = "providers.tf"
  if_exists = "overwrite"
  contents = templatefile("providers.tmpl", {
    aws_region     = local.aws_region
    child_accounts = local.child_accounts
  })
}
```

And then, in `providers.tmpl`, we used a `for` directive to generate these `provider` blocks as follows:

```hcl
%{ for name, account in child_accounts ~}
provider "aws" {
  region = "${aws_region}"
  alias  = "${name}"
}
%{~ endfor }
```
***

**yorinasub17** commented *Sep 27, 2021*

Closing this as the original question has been answered. If there are any follow ups, please open a new ticket with updated context. Thanks!
***

**gyoza** commented *Jul 11, 2023*

Having trouble using this with latest terragrunt and terraform. 

```
ERRO[0000] terragrunt.hcl:146,14-27: Error in function call; Call to function "templatefile" failed: _generated_providers.tmpl:2,8-9: Invalid template interpolation value; Cannot include the given value in a string template: string required., and 4 other diagnostic(s)., and 1 other diagnostic(s)

```

when copying the above code..

If you pass a basic list to the template function it works but then im in the territory of string manipulation to do a simple template... 

Maybe I am doing something wrong here..
***

