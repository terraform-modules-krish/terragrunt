# Possibility to generate required_providers ?

**ChristophShyper** commented *Sep 28, 2020*

I don't see any documented way of generating `required_providers` of `terraform` block.

Previously I had something like this for each of my normal `terragrunt.hcl` files:
```hcl
generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite"
  contents  = <<EOF
provider "aws" {
  region  = "${local.aws_default_region}"
  version = "${local.aws_provider_version}"
}
provider "external" {
  version = "${local.external_provider_version}"
}
provider "template" {
  version = "${local.template_provider_version}"
}
provider "null" {
  version = "${local.null_provider_version}"
}
EOF
}
```
 
My main `terragrunt.hcl` had something like:
```hcl
remote_state {
  backend = "s3"

  config = {
    encrypt          = true
    bucket           = local.table_name
    key              = "terragrunt/${local.repository_name}/${path_relative_to_include()}/terraform.tfstate"
    region           = "${local.aws_default_region}"
    dynamodb_table   = "${local.repository_name}-terraform-locks-${local.aws_default_region}"
    force_path_style = true
  }

  # Overwrite backend configuration file for each module
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite"
  }
}
```

How to do it all now with Terraform 0.13? 
Now first part doesn't have information about versions of providers.
But what about `required_providers` part? It should be together with backend configuration in the second part. But how to achieve it?

<br />
***


**brikis98** commented *Oct 5, 2020*

Is the concern you have that you would end up with two `terraform { ... }` blocks, one for `backend` config and one for `required_providers`? I _thought_ Terraform allowed that, but would you mind checking?
***

**ChristophShyper** commented *Oct 5, 2020*

My concern is there is no support for `required_providers` at all.

Haven't checked if that trick works. It should be a proper HCL code if both blocks with the same name are in the same file. But I don't know what happens if they are in separate files. If I'll have some time some other day I'll check it out.

As a workaround I rewritten all my `terragrunt.hcl` files with something like: 
```terraform
generate "providers" {
  path      = "versions.tf"
  if_exists = "overwrite"
  contents  = <<EOF
# PROVIDER AND BACKEND DEFINITION

terraform {
  backend "s3" {}

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "${local.aws_provider_version}"
    }

    external = {
      source  = "hashicorp/external"
      version = "${local.external_provider_version}"
    }

    null = {
      source  = "hashicorp/null"
      version = "${local.null_provider_version}"
    }

    template = {
      source  = "hashicorp/template"
      version = "${local.template_provider_version}"
    }
  }
}

provider "aws" {
  region  = "${local.aws_default_region}"
}

provider "external" {}

provider "null" {}

provider "template" {}
EOF
}
```

So my root `terragrunt.hcl` has now only:
```terraform
remote_state {
  backend = "s3"

  config = {
    encrypt          = true
    bucket           = local.table_name
    key              = "terragrunt/${local.repository_name}/${path_relative_to_include()}/terraform.tfstate"
    region           = "${local.aws_default_region}"
    dynamodb_table   = "${local.repository_name}-terraform-locks-${local.aws_default_region}"
    force_path_style = true
  }
}
```

But I prefered to have all those parameters in files rather than passed as arguments. 
***

**brikis98** commented *Oct 6, 2020*

> But I prefered to have all those parameters in files rather than passed as arguments.

What do you mean?
***

**ChristophShyper** commented *Oct 9, 2020*

If backend configuration is defined with `generate = {}` block then it's not visible in build log. When debugging locally I can just go to the directory and work on it like a normal terraform code. But when it's passed as arguments then there is additional work. That's not a deal-breaker, but just slight inconvenience.

Now, after I refactored the whole repository  to use proxied providers it needs much less code (I have a lot of submodules), so it's kind of DRY.

But still it would be nice to have provider definition dealt with a predefined new configuration block, rather than using `generate "provider"` with EOF, it's not an elegant solution if it's something that is a main configuration in Terraform 0.13.
It would be nice to have all providers defined in main `terragrunt.hcl`, but later pass only the required ones in other `terragrunt.hcl` files.
***

**brikis98** commented *Oct 12, 2020*

> But still it would be nice to have provider definition dealt with a predefined new configuration block, rather than using `generate "provider"` with EOF, it's not an elegant solution if it's something that is a main configuration in Terraform 0.13.
> It would be nice to have all providers defined in main `terragrunt.hcl`, but later pass only the required ones in other `terragrunt.hcl` files.

On the one hand, first-class support for `provider` blocks and many other features of Terraform would be more convenient... But on the other, it takes work to build that, and even more work to maintain it, and continuously chase all the new features and changes in Terraform. One of the charms of `generate` is that it tends to be general purpose, with this same functionality handling many features of Terraform, both present and future. We'll need to think carefully about which direction we want to go here.
***

**kieranbrown** commented *Oct 22, 2020*

This can be achieved currently using terraform's override files functionality which can merge the `required_providers` block.

https://www.terraform.io/docs/configuration/override.html#merging-terraform-blocks

I'm not a terragrunt/terraform expert but the below example is working fine for me on 0.13:

```hcl
generate "versions" {
  path      = "versions_override.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
    terraform {
      required_providers {
        cloudflare = {
          source = "cloudflare/cloudflare"
          version = "2.12.0"
        }
      }
    }
EOF
}
```
***

**ChristophShyper** commented *Jul 21, 2021*

I guess few workarounds is enough to consider this not an issue anymore.
***

