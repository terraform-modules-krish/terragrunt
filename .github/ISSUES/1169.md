# plan-all loads `terragrunt.hcl` config from downloaded module

**zachwhaley** commented *May 7, 2020*

When running `terragrunt plan-all` the resulting `terraform` command does not include arguments from `extra_arguments` defined in the root or module `terragrunt.hcl` config.

E.g. If I `cd` to an individual module and run `terragrunt plan`, the resulting `terraform` command is 

```
terraform plan -var env=staging -var region=us-east-1 -var ticket=TIP-UP
```

I would assume running `terragrunt plan-all` from the root directory would yield the same `terraform` command for that module but it doesn't, it is just 

```
terraform plan
```

The plan-all then fails because certain required arguments are not set.

terragrunt version v0.23.14
Terraform v0.12.24
<br />
***


**brikis98** commented *May 10, 2020*

Hm, I wonder if that's based on the list of commands you pass in, and Terragrunt mistakingly treating the command as `plan-all` rather than `plan`? Could you share some of your `terragrunt.hcl` code?
***

**zachwhaley** commented *May 12, 2020*

Here is a redacted version of our root `terragrunt.hcl`

```terraform
locals {
  env    = get_env("foo_env", "ENV_NOT_SET")
  region = get_env("AWS_DEFAULT_REGION", "REGION_NOT_SET")
}

remote_state {
  backend = "s3"
  config = {
    bucket         = "bucket-${local.env}-${local.region}"
    key            = "${path_relative_to_include()}/terraform.tfstate"
    region         = local.region
    encrypt        = true
    dynamodb_table = "terraform-state-locking"
  }
  disable_init = tobool(get_env("TERRAGRUNT_DISABLE_INIT", "false"))
}

inputs = {
  user             = get_env("USER", "nobody")
  account_id       = get_aws_account_id()
  color            = get_env("bar", "green")
}

terraform {
  extra_arguments "init" {
    commands = ["init"]
    arguments = [
      "-reconfigure",
    ]
  }

  extra_arguments "vars" {
    commands = get_terraform_commands_that_need_vars()
    arguments = [
      "-var", "env=${local.env}",
      "-var", "region=${local.region}",
      "-var", "ticket=${get_env("foo_ticket", "NA")}",
    ]
  }
}
```

And a module `terragrunt.hcl`

```terraform
include {
  path = find_in_parent_folders()
}

locals {
  env = get_env("foo_env", "ENV_NOT_SET")
}

dependencies {
  paths = [
    "../a",
    "../b",
    "../c",
    "../d",
  ]
}

terraform {
  extra_arguments "foo_vars" {
    commands = get_terraform_commands_that_need_vars()
    required_var_files = [
      "${get_terragrunt_dir()}/abc.tfvars",
      "${get_parent_terragrunt_dir()}/a/abc.tfvars",
      "${get_parent_terragrunt_dir()}/config.json",
      "${get_parent_terragrunt_dir()}/${local.env}-manifest.json",
    ]
  }
}
```
***

**brikis98** commented *May 31, 2020*

Apologies for the long delay. I was only able to get back to this now. Unfortunately, I'm not able to repro with either Terragrunt v0.23.0 or v0.23.23. Here's a minimal repro I tried to create:

```
$ tree
.
├── child1
│   ├── foo.tfvars
│   ├── main.tf
│   └── terragrunt.hcl
├── child2
│   ├── foo.tfvars
│   ├── main.tf
│   └── terragrunt.hcl
└── terragrunt.hcl
```

Root `terragrunt.hcl`:

```hcl
terraform {
  extra_arguments "vars" {
    commands = get_terraform_commands_that_need_vars()
    arguments = [
      "-var", "foo=FROM-ROOT"
    ]
  }  
}
```

Child `terragrunt.hcl`:

```hcl
include {
  path = find_in_parent_folders()
}

terraform {
  extra_arguments "foo_vars" {
    commands = get_terraform_commands_that_need_vars()
    required_var_files = [
      "${get_terragrunt_dir()}/foo.tfvars"
    ]
  }
}
```

`main.tf`:

```hcl
variable "foo" {
  type = string
}

variable "bar" {
  type = string
}

resource "random_pet" "name" {
  keepers = {
    recreate_always = timestamp()
  }

  prefix    = "${var.foo}-${var.bar}"
  length    = 10
  separator = "-"
}

output "foo" {
  value = var.foo
}

output "bar" {
  value = var.bar
}
```

And `foo.tfvars` contains `bar = "child1"` or `bar = "child2"` as appropriate. Whether I run `plan` within one module or `plan-all` at the root, I get the exact same output: `terraform` is executed with the expected `-var` and `-var-file` arguments and the `prefix` for the `random_pet` resource is "FROM-ROOT-from-child<X>" where `X` is 1 or 2, as appropriate.

Is there something this minimal repro is missing that your code has?
***

**zachwhaley** commented *Jun 1, 2020*

Taking another look at my output it is actually our downloaded modules in `.terraform` directories that are running `terraform plan` and failing.
We add empty `terragrunt.hcl` files to our modules so that we can run `terragrunt validate-all` on them in PR checks.

I've reproduced this with the following code:

Tree structure:
```
.
├── child
│   ├── main.tf
│   └── terragrunt.hcl
└── terragrunt.hcl
```

`./terragrunt.hcl`
```terraform
inputs = {
  user = get_env("USER", "nobody")
}

terraform {
  extra_arguments "vars" {
    commands = get_terraform_commands_that_need_vars()
    arguments = [
      "-var", "foo=FROM-ROOT"
    ]
  }
}
```

`child/terragrunt.hcl`
```terraform
include {
  path = find_in_parent_folders()
}
```

`child/main.tf`
```terraform
variable "user" {
  type = string
}

variable "foo" {
  type = string
}

module "pet_name" {
  source = "github.com/zachwhaley/tg-mods"
  name   = var.user
}

resource "random_pet" "name" {
  keepers = {
    recreate_always = timestamp()
  }

  prefix    = module.pet_name.name
  length    = 10
  separator = "-"
}

output "foo" {
  value = var.foo
}
```

Running `terragrunt plan` from `child` directory ignores `.terraform/modules` but running `terragrunt plan-all` from root directory does not ignore `.terraform/modules`.

```
$ terragrunt plan-all
...
[terragrunt] [/Users/zacharyw/code/tg-test/child] 2020/06/01 10:31:17 Running command: terraform plan -var foo=FROM-ROOT
[terragrunt] [/Users/zacharyw/code/tg-test/child/.terraform/modules/pet_name] 2020/06/01 10:31:17 Running command: terraform plan
var.name
  Enter a value:
```
***

**brikis98** commented *Jun 2, 2020*

Ohhh. OK, I think I see what's happening:

1. First, you run `terragrunt plan` in the `child` folder, it runs `terraform init` and `terraform plan` in that same older. The `user` input variable is set through Terragrunt (through the `input` block in the root `terragrunt.hcl`) and passed through to the `name` parameter of `github.com/zachwhaley/tg-mods`, so you're not prompted for any inputs.
1. Next, you run `terragrunt plan-all` in the root folder, and it finds _two_ modules to deploy: `child` and `child/.terraform/modules/pet_name`. The latter was downloaded as part of the `terraform init` in the previous step—it's the github.com/zachwhaley/tg-mods module! Since that module has a `terragrunt.hcl`, Terragrunt tries to execute it as its own standalone module during `plan-all`. Since that `terragrunt.hcl` file is empty, no variables are being set for that one, so it prompts you for `var.name`. 

 So it has nothing to do with `extra_arguments` or anything like that... It's just this extra `terragrunt.hcl` file getting picked up from `.terraform`. This is probably something we should improve in the [`containsTerragruntModule`](https://github.com/gruntwork-io/terragrunt/blob/938cf8c1c288b1b14919e48cbfce7801a3778dcd/config/config.go#L263) method, skipping not only the [Terragrunt cache dir](https://github.com/gruntwork-io/terragrunt/blob/938cf8c1c288b1b14919e48cbfce7801a3778dcd/config/config.go#L268-L271) but also the Terraform cache dir. Would you be up for a PR to fix that?
***

**zachwhaley** commented *Jun 2, 2020*

> Would you be up for a PR to fix that?

Sure!  I can make a go at it.
***

**zachwhaley** commented *Jun 2, 2020*

Also, would you like me to change this title since the bug had nothing to do with extra_arguments?
***

**brikis98** commented *Jun 2, 2020*

> Sure! I can make a go at it.

Thanks!

> Also, would you like me to change this title since the bug had nothing to do with extra_arguments?

Yes please!
***

