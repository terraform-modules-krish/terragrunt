# Unable to use generate block to write complex map(object) to a file

**itsnagaraj** commented *Feb 16, 2023*

We have defined a module where one of the variable is of type map(object)

```
variable "topics" {
  description = "List of topics and its configuration"
  type = map(object({
    name               = string
    config             = map(string)
  }))
}
```

We have extensively used terragrunt to declare inputs to this module and we have reached a point where following error is thrown by the terraform

`fork/exec /root/.terraforms/terraform-0.14.2/terraform: argument list too long`

Based on the suggestion in this [issue](https://github.com/gruntwork-io/terragrunt/issues/2132), we decided to use the generate block to generate tfvars file and use that as input instead of default mechanism of inputs being passed as environment variables to terraform. Below is the terragrunt file `staging.hcl` that does the generation

```
terraform {
  source = "${path_relative_from_include()}/..//modules/topics/"

  extra_arguments "terragrunt_generated_vars" {
    commands              = "${get_terraform_commands_that_need_vars()}"
    required_var_files = [
      "topics.auto.tfvars",
    ]
  }
}

locals {
  topic_vars = read_terragrunt_config(find_in_parent_folders("topics.hcl"))
}

generate "topics" {
  path      = "topics.auto.tfvars"
  if_exists = "overwrite"
  contents  = <<EOF
  topics = {
    "${local.topic_vars.inputs.topics}"
  }

EOF
}

```

Below are the contents of `topics.hcl`

```
inputs = {
   topics = {
      "topic-1" = {
        name               = "test"
        config = {
          "policy_key" = "policy_value"
        }
      }
    }
}
```

When we run `terragrunt plan` command we get the following error

`Invalid template interpolation value; Cannot include the given value in a string template: string required.`

But we don't get the same error when the contents of `topics.hcl` are placed inline in the `staging.hcl` file. Any suggestions/clues to avoid this error? 

Terragrunt version: 0.29.0
Terraform version: 0.14.2




<br />
***


**propyless** commented *Feb 16, 2023*

You are referencing a map of objects not a string. .topics = {} so thats why you are getting that error.

I see that you refererenced the issue I also had a few days ago..

What I've done instead  is that dumped all the vars into a vars file
```
generate "vars_input" {
  path              = "variables.auto.tfvars.json"
  if_exists         = "overwrite"
  disable_signature = true
  contents = jsonencode(merge(
    local.merged_locals,
    local.tags
  ))
}


```

I think a json file is cleaner than `env_key = value` and more appropriate for map values.
***

**itsnagaraj** commented *Feb 17, 2023*

> You are referencing a map of objects not a string. .topics = {} so thats why you are getting that error.
> 
> I see that you refererenced the issue I also had a few days ago..
> 
> What I've done instead is that dumped all the vars into a vars file
> 
> ```
> generate "vars_input" {
>   path              = "variables.auto.tfvars.json"
>   if_exists         = "overwrite"
>   disable_signature = true
>   contents = jsonencode(merge(
>     local.merged_locals,
>     local.tags
>   ))
> }
> ```
> 
> I think a json file is cleaner than `env_key = value` and more appropriate for map values.

Thanks for the suggestion and appreciate your help. It makes sense to use JSON file for map objects. This worked and we no longer see the following error

`fork/exec /root/.terraforms/terraform-0.14.2/terraform: argument list too long`

Also noticed that when using `local.variable-name` additional attributes were added to the JSON file and the inputs where nested inside other objects but when I switched to using `local.variable-name.inputs` the JSON file had just the inputs and in correct structure. This could probably because of how have structured by locals and inputs.
***

