# Referencing ${var.} variable in the 'locals' section fails: you can only reference other local variables here

**mehighlow** commented *Aug 13, 2022*

**Env:**
Terraform v1.2.6
terragrunt version v0.38.6

**What I'm trying to achieve:**

Whenever I run terragrunt I want to specify a file that represents a set of valuables for a particular env(I've got 30+ of them) as an input var:
e.g.: `terragrunt init -var "cluster=path/to/cluster1.yaml"`

terragrunt.hcl:
```shell
locals {
  cluster = yamldecode(file("${var.cluster}"))
}

remote_state {
  backend = "azurerm"
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite"
  }
  config = {
    subscription_id      = local.cluster.subscription_id
    resource_group_name  = local.cluster.storage_resource_group
    storage_account_name = local.cluster.storage_account
    container_name       = local.cluster.aks.name
    key                  = "terraform.tfstate"
  }
}

inputs = {
  resource_group     = local.cluster.resource_group.name
  name               = local.cluster.aks.name
  kubernetes_version = local.cluster.aks.version
  location           = local.cluster.aks.location
}

terraform {
  source = "${get_repo_root()}/terraform/modules/aks_cluster"
}

```
variables.tf:
```shell
variable "cluster" {
  description = "Path to cluster file definition"
  type        = string
}
```
The code above fails with `Can't evaluate expression at ./terragrunt.hcl:2: you can only reference other local variables here, but it looks like you're referencing something else (var is not defined)]`

with explicitly specified file, e.g.:
```shell
locals {
  cluster      = yamldecode(file("path/to/file.yaml"))
}
...
```
everything works as a charm.

I do understand that this might be somehow related to a chicken-egg problem. But, maybe, there is a room for terragrunt env vars, i.e. `TG_VAR_name`?

<br />
***


**mehighlow** commented *Aug 13, 2022*

The workaround I ended up with as of now:

```shell
export CLUSTER="path/to/file.yaml"
```

cat echo_cluster_path.sh:
```shell
#!/usr/bin/env sh
echo $CLUSTER
```
terragrunt.hcl:
```shell
locals {
  cluster = yamldecode(file(run_cmd("./echo_cluster_path.sh")))
}
...
```
***

**denis256** commented *Aug 14, 2022*

Hi,
was considered to use `get_env` function to fetch configuration file?


```
# terragrunt.hcl
locals {
  cluster = yamldecode(file(get_env("CONFIG", "conf1.yaml")))
  test = run_cmd("echo", jsonencode(local.cluster))
}
```

Execution:
```
CONFIG=conf1.yaml terragrunt apply
CONFIG=conf2.yaml terragrunt apply
```
References:

https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#get_env

https://github.com/denis256/terragrunt-tests/tree/master/env-yaml-decode
***

**mehighlow** commented *Aug 14, 2022*

Hi @denis256 ,

Thanks a lot. Your solution works as a charm!
***

