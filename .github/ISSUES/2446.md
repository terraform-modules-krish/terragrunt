# Terragrunt with multiple org-accounts

**kevinashaw** commented *Feb 11, 2023*

We need to manage multiple parallel AWS organizational accounts.  Due to country-by-country data privacy requirements, we need to implement exact duplicate AWS infrastructures in separate accounts/regions.  We first built our infrastructure by hand in one account, then coded that in terraform and used it to build a second account.  This worked well, but we are finding that terraform is tricky (if not downright unmanageable) with multiple accounts.  So we are exploring TerraGrunt -- especially after reading Mr Brikman's excellent book on Terraform.  TG seems like a solid solution but I must be missing something regarding backend bucket-key management.

We want to store Terraform state for each org-account, each environment and each stack separately. For example, our current s3 bucket keys look like:
* `product-name/account-name-1/stage-name/iams/terraform.tfstate`
* `product-name/account-name-1/stage-name/vpc/terraform.tfstate`
* `product-name/account-name-1/stage-name/dns/terraform.tfstate`
And
* `product-name/account-name-2/stage-name/iams/terraform.tfstate`
* `product-name/account-name-2/stage-name/vpc/terraform.tfstate`
* `product-name/account-name-2/stage-name/dns/terraform.tfstate`
Etc.

The code for each account-name will be *identical*, while the stage variants will be kept in different git branches. So we will never have duplicate code within a given branch. 
Somehow we need to keep separate state for each account-name, each stage (prod/dev/staging) and each stack.  Note that we have about 20+ stacks to build the system.
TG is helpful with the `${path_relative_to_include()}` command, but it seems that TG does not allow for variable interpolation within the key.  
Question: How can we can we parameterize the bucket-key?

Here is the `remote_state` block of our `terragrunt.hcl` file.  The code is wrong, but it implements what would make sense to me.
```
remote_state {
  backend = "s3"
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite_terragrunt"
  }
  config = {
    bucket   = "my-terraform-bucket"
    key      = "my-product/${var.org_account_name}/${var.env_stage}/${path_relative_to_include()}/terraform.tfstate"
    region   = var.org_account_region
    encrypt  = true
    dynamodb_table = "TerraformStateLock"
  }
}

```
<br />
***


**lorengordon** commented *Feb 12, 2023*

What kind of error are you getting? I use this kind of interpolation the in the key all the time. Something like this is pretty standard for me...

```
remote_state {
  backend = "s3"

  config = {
    profile = local.profile_name

    bucket         = local.tags.TfstateBucket
    dynamodb_table = local.tags.TfstateLockTable
    encrypt        = true
    key            = local.tags.TfstateKey
    region         = local.region

    dynamodb_table_tags = local.tags
    s3_bucket_tags      = local.tags
  }

  generate = {
    path      = "backend.tf"
    if_exists = "overwrite_terragrunt"
  }
}

locals {
  repo_name = basename(abspath("${get_parent_terragrunt_dir()}/../.."))

  tags = {
    TfstateBucket    = "${local.repo_name}-tfstate"
    TfstateKey       = "tfstate/stacks/${path_relative_to_include()}/terraform.tfstate"
    TfstateLockTable = "${local.repo_name}-tfstate-lock"
  }
}
```
***

**kevinashaw** commented *Feb 12, 2023*

@lorengordon Thank you for the response.  I am new to Terragrunt and trying to understand the syntax from the tutorials and stackoverflow articles.  It must be that I didn't search hard enough, as I haven't seen examples of this.  A number of SO articles stated that this was just not possible.  They must be dated, but its hard to tell what is still an issue and what has been fixed.  Perhaps an example in the tutorial showing interpolation with local variables would clarify matters.
Thank you again!  This is very helpful!
***

**lorengordon** commented *Feb 12, 2023*

@kevinashaw If you tried your exact config, I think the main problem you'd hit is that "var" references refer to _terraform_ variables. Terragrunt itself has no concept of variables, separate from terraform. And _terraform_ does not allow variable references in backend configs.

>A backend block cannot refer to named values (like input variables, locals, or data source attributes).
>https://developer.hashicorp.com/terraform/language/settings/backends/configuration#using-a-backend-block

But using Terragrunt, you do have access to most of the [functions](https://developer.hashicorp.com/terraform/language/functions) that terraform offers, as well as [several unique to terragrunt](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/). And Terragrunt does let you use interpolation in the terragrunt `remote_state` block. So, you can replace terraform variables with terragrunt interpolation and functions, to get where I think you're trying to go. The approach will just have to look a little different than what you started with. You could maybe get values from environment variables (`get_env()`), or a hierarchical directory structure (many functions), or an external command (`run_cmd()`), or an external config file (`yamldecode(file())`), etc...
***

