# Why do you call it "before_hook" if inputs executes before "before_hook" anyway? What the point?

**bankinobi** commented *Nov 17, 2020*

Hello!

I am trying to setup secret management with terraform + terragrunt + hashicorp vault.
In current state i have an empty secrets.yaml file in repo with "mock:" string inside it to prevent fails with empy yaml file errors.
And i have a "before_hook" which executes a script that reads data from vault storage and write it into secrets.yaml file with proper formatting.

Here is how my terragrunt.hcl file looks like:


```
locals {
  region_vars = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  region      = local.region_vars.locals.region
}

dependencies {
  paths = ["../gke"]
}

dependency "gke" {
  config_path = "../gke"
}

include {
  path = find_in_parent_folders()
}

terraform {
  source = "/path/to/terraform-modules/k8s"

  before_hook "read_vault" {
    commands = ["plan", "apply", "destroy", "refresh"]
    execute  = ["./secrets.sh", "read-secrets", "${get_terragrunt_dir()}"]
  }
  after_hook "delete_secrets" {
    commands     = ["plan", "apply", "destroy", "refresh"]
    execute      = ["./secrets.sh", "delete-secrets", "${get_terragrunt_dir()}"]
    run_on_error = true
  }
}

inputs = {
  k8s_secrets            = yamldecode(file("${get_terragrunt_dir()}/secrets.yaml"))
  endpoint               = dependency.gke.outputs.endpoint
  cluster_ca_certificate = dependency.gke.outputs.cluster_ca_certificate
}
```

Before hook works perfectly, script generates valid yaml file with all necessary secrets.
But inputs anyway already executed "yamldecode" with secrets.yaml that contains only "mock:" string inside =(

Outputs with var.k8s_secrets after refresh
```
Outputs:

debug = {}

```
<br />
***


**brikis98** commented *Nov 18, 2020*

It's a before hook in that it runs before your Terraform command does (e.g., before `terraform apply`). But, as you've noticed, the parsing of the config file happens first—as we must parse the file to find the `before_hook` in the first place!—and the hooks do not affect that parsing. That said, you could write your secrets to a `terraform.tfvars` file and Terraform will pick those up  automatically.
***

**bankinobi** commented *Nov 18, 2020*

Nice suggestion, but I'm trying to get away from the stillborn *.tfvars and organize all values with pleasant templatable yamls. And I've been doing great up to this moment.
Auto load of terraform.tfvars is a cool option, but not obvious and it will probably be difficult for another engineer who reads the config to immediately understand where the values for secrets comes from.
I was hoping there was a way to re-read the inputs after performing the hooks, which I don't see in the documentation.
Well, then we're doing dirt with *.tfvars format =(
Thank you for your reply, @brikis98 
***

**brikis98** commented *Nov 18, 2020*

I have a suggestion for you actually: use [`run_cmd`](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#run_cmd) directly! That is, create a script that reads your secrets and writes the plaintext data, as YAML, to `stdout`. Then you can do something like this:

```hcl
inputs =  {
  k8s_secrets = yamldecode(run_cmd("./read-secrets"))
}
```

This keeps the plaintext secrets off disk too, and no files to mess with.
***

**brikis98** commented *Nov 18, 2020*

For other alternatives, see: https://blog.gruntwork.io/a-comprehensive-guide-to-managing-secrets-in-your-terraform-code-1d586955ace1
***

**bankinobi** commented *Nov 19, 2020*

> I have a suggestion for you actually: use [`run_cmd`](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#run_cmd) directly! That is, create a script that reads your secrets and writes the plaintext data, as YAML, to `stdout`. Then you can do something like this:
> 
> ```terraform
> inputs =  {
>   k8s_secrets = yamldecode(run_cmd("./read-secrets"))
> }
> ```
> 
> This keeps the plaintext secrets off disk too, and no files to mess with.

Perfect solution. Works like a charm. Thank you!
***

**rdettai** commented *Aug 12, 2022*

for further reference, using `run_cmd` doesn't work for me as it is also evaluated eagerly when the terragrunt files are parsed
***

