# [Question] How to only `generate` the (actually) required 'providers' with Terragrunt?

**oatmealb** commented *Jul 5, 2022*

If a particular provider (say `"hashicorp/kubernetes"`) is not required to be in `required_providers` in _all_ but only particular environments (say "Staging"), how can we avoid duplication?

F.e., I have the following `generate` block in the **root** `terragrunt.hcl`, which then causes _every_ leaf in the dependency graph to have `kubernetes` in its `.terraform.lock.hcl`. How can I have it _not_ listed in environments where it isn't actually used?

```hcl
# root terragrunt.hcl
    generate "versions" {
      path      = "versions.tf"
      if_exists = "overwrite_terragrunt"
      contents  = <<EOF
        terraform {
          required_version = "1.1.4"
          required_providers {
            aws = {
              source  = "hashicorp/aws"
              version = "4.18.0"
            }
            # This provider is only needed in some environments.
            kubernetes = {
              source  = "hashicorp/kubernetes"
              version = "2.11.0"
            }
          }
        }
    EOF
    }
```

The above `generate` block _could_ be placed in an **intermediary/per-environment** `terragrunt.hcl` and have it overwrite **the root** `terragrunt.hcl`'s `versions.tf`. But this would mean that the _other_, actually required-everywhere providers would have to be duplicated - only `"hashicorp/aws"` in this example, but there could be more.

In short, is there a way to have the following snippet only in environments that need it?
```hcl
            kubernetes = {
              source  = "hashicorp/kubernetes"
              version = "2.11.0"
            }
```
<br />
***


**yorinasub17** commented *Jul 5, 2022*

Currently there are two ways to influence the behavior of the parent blocks:

- Inheritance override in a merge operation
- Folder structure based overrides

For this use case, the latter is probably what you want.

For example, you could define a bunch of flags to control the parent file. Consider a file called `default_flags.hcl` in the root folder:

```hcl
locals {
  use_kubernetes_provider = true
}
```

And then for you can define a file `flags.hcl` to override this for specific folders:

```hcl
locals {
  use_kubernetes_provider = false
}
```

Finally in the root config, you can do something like this to make use of all of this:

```hcl
locals {
  flags = read_terragrunt_config(find_in_parent_folders("flags.hcl", find_in_parent_folders("default_flags.hcl")))
}

generate "versions" {
  path      = "versions.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
terraform {
  required_version = "1.1.4"
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "4.18.0"
    }

%{ if local.flags.locals.use_kubernetes_provider }
  kubernetes = {
    source  = "hashicorp/kubernetes"
    version = "2.11.0"
  }
%{ endif }
}
EOF

}
```
***

**oatmealb** commented *Jul 7, 2022*

Thanks Yoriyasu! This works and is marked as `(A)` on the below diagram. Now, correct me please if the following is incorrect, but if we wanted option `(B)` on this diagram, that is, to have the `use_kubernetes_provider` flag set at a **leaf**, not intermediary, level, then all we need is a `locals` block in that leaf's `terragrunt.hcl`. (It seems to work - I'm only being cautious.)

Apologies to not closing this sooner, but I only now satisfactorily understand [find_in_parent_folders](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/#find_in_parent_folders). (A side remark on its docs: it's a bit confusing to refer to an "optional `name` parameter" as it's not clear (to me at least) where the `name` comes from - quick glance at [its source code](https://github.com/gruntwork-io/terragrunt/blob/aa552aa8eaf3a2d2ae20a2898f92ecd1a1bc0967/config/config_helpers.go#L529) doesn't reveal anything as well.)

```bash
├── default_flags.hcl  # locals { use_kubernetes_provider = false }
├── terragrunt.hcl
└── prod
    └── account.hcl
    └── flags.hcl  # (A) locals { use_kubernetes_provider = true }
    └── k8s
        └── terragrunt.hcl  # (B)
```


***

**yorinasub17** commented *Jul 7, 2022*

> Now, correct me please if the following is incorrect, but if we wanted option (B) on this diagram, that is, to have the use_kubernetes_provider flag set at a leaf, not intermediary, level, then all we need is a locals block in that leaf's terragrunt.hcl. (It seems to work - I'm only being cautious.)

That unfortunately won't work because the `locals` don't merge up. That is, `locals` from the child are not visible in the parent. Ideally you can use `fileexists` with a ternary to handle this, but the current HCL parser doesn't terminate ternary expressions, so you would run into an error.

> A side remark on its docs: it's a bit confusing to refer to an "optional name parameter" as it's not clear (to me at least) where the name comes from - quick glance at [its source code](https://github.com/gruntwork-io/terragrunt/blob/aa552aa8eaf3a2d2ae20a2898f92ecd1a1bc0967/config/config_helpers.go#L529) doesn't reveal anything as well.

That's fair. We've gone through a few iterations on this so if you can suggest an improvement, that would be great!
***

