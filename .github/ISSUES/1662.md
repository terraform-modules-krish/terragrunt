# [dependency] terragrunt plan output Invalid resource error message with mock_outputs set

**jeff51419** commented *Apr 29, 2021*

when i use the "terragrunt run-all plan" on "vpc peering" and "vpc" which i already set up mock_outputs on vpc peering for each vpc-id.
it will comes error InvalidVpcID.NotFound. it should be use fake-stp-vpc-id i prepared.

```
Error: InvalidVpcID.NotFound: The vpc ID 'fake-stp-vpc-be-id' does not exist
│ 	status code: 400, request id: 6ac09ebc-7293-44c1-8aa7-3eaabacd6809
│
│   with module.vpc-peering-pub2be.data.aws_vpc.acceptor[0],
│   on .terraform/modules/vpc-peering-pub2be/main.tf line 33, in data "aws_vpc" "acceptor":
│   33: data "aws_vpc" "acceptor" {
```

![image](https://user-images.githubusercontent.com/18411883/116519285-e6f8e780-a903-11eb-8170-9f326a49da29.png)

but I can terragrunt run-all apply on vpc-peering

terragrunt.hcl
```hcl
locals {
  # Automatically load environment-level variables
  environment_vars = read_terragrunt_config(find_in_parent_folders("env.hcl"))
  project_vars     = read_terragrunt_config(find_in_parent_folders("project.hcl"))
  region_vars      = read_terragrunt_config(find_in_parent_folders("region.hcl"))


  # Extract out common variables for reuse
  env         = local.environment_vars.locals.environment
  aws_project = local.project_vars.locals.aws_project
  aws_region  = local.region_vars.locals.aws_region
}

# Terragrunt will copy the Terraform configurations specified by the source parameter, along with any files in the
# working directory, into a temporary folder, and execute your Terraform commands in that folder.
# git@github.com:terraform-aws-modules/terraform-aws-vpc.git

terraform {
  source = "${get_terragrunt_dir()}/../../../../../../terraform/module//vpc-peering"
}


# Include all settings from the root terragrunt.hcl file
include {
  path = find_in_parent_folders()
}

# Passing outputs for vpc-be modules
dependency "vpc-be" {
  config_path = "../vpc-be"
  
  # it corresponds to a map that will be injected in place of the actual dependency outputs 
  # if the target config hasn’t been applied yet
  mock_outputs = {
    vpc-be-id  = "fake-stp-vpc-be-id"
  }
  # restrict this behavior
  mock_outputs_allowed_terraform_commands = ["validate","plan","init"]
}

# Passing outputs for vpc-public modules
dependency "vpc-pub" {
  config_path = "../vpc-pub"
  
  # it corresponds to a map that will be injected in place of the actual dependency outputs 
  # if the target config hasn’t been applied yet
  mock_outputs = {
    vpc-pub-id  = "fake-stp-vpc-pub-id"
  }
  # restrict this behavior
  mock_outputs_allowed_terraform_commands = ["validate","plan","init"]
}

# Passing outputs for vpc-db modules
dependency "vpc-db" {
  config_path = "../vpc-db"
  
  # it corresponds to a map that will be injected in place of the actual dependency outputs 
  # if the target config hasn’t been applied yet
  mock_outputs = {
    vpc-db-id  = "fake-stp-vpc-db-id"
  }
  # restrict this behavior
  mock_outputs_allowed_terraform_commands = ["validate","plan","init"]
}

# These are the variables we have to pass in to use the module specified in the terragrunt configuration above
inputs = {
  vpc_be_id   = dependency.vpc-be.outputs.vpc-be-id
  vpc_pub_id  = dependency.vpc-pub.outputs.vpc-pub-id
  vpc_db_id   = dependency.vpc-db.outputs.vpc-db-id
  
  project     = "${local.aws_project}"
  environment = "${local.env}"
  region      = "${local.aws_region}"
}
```

vpc-peering module
```hcl
module "vpc-peering-be2db" {
  source  = "cloudposse/vpc-peering/aws"
  version = "0.9.0"

  namespace = "stp"
  stage     = var.environment
  name      = "be2db"

  auto_accept                               = true
  requestor_allow_remote_vpc_dns_resolution = false
  acceptor_allow_remote_vpc_dns_resolution  = true
  requestor_vpc_id                          = var.vpc_be_id
  acceptor_vpc_id                           = var.vpc_db_id
  create_timeout                            = "5m"
  update_timeout                            = "5m"
  delete_timeout                            = "10m"

  tags = {
    Name        = "vpc-peering-be2db"
    Project     = var.project
    Environment = var.environment
    Module      = var.module
  }
}

module "vpc-peering-pub2db" {
  source  = "cloudposse/vpc-peering/aws"
  version = "0.9.0"

  namespace = "stp"
  stage     = var.environment
  name      = "pub2db"

  auto_accept                               = true
  requestor_allow_remote_vpc_dns_resolution = false
  acceptor_allow_remote_vpc_dns_resolution  = true
  requestor_vpc_id                          = var.vpc_pub_id
  acceptor_vpc_id                           = var.vpc_db_id
  create_timeout                            = "5m"
  update_timeout                            = "5m"
  delete_timeout                            = "10m"

  tags = {
    Name        = "vpc-peering-pub2db"
    Project     = var.project
    Environment = var.environment
    Module      = var.module
  }
}

module "vpc-peering-pub2be" {
  source  = "cloudposse/vpc-peering/aws"
  version = "0.9.0"

  namespace = "stp"
  stage     = var.environment
  name      = "pub2be"

  auto_accept                               = true
  requestor_allow_remote_vpc_dns_resolution = true
  acceptor_allow_remote_vpc_dns_resolution  = true
  requestor_vpc_id                          = var.vpc_pub_id
  acceptor_vpc_id                           = var.vpc_be_id
  create_timeout                            = "5m"
  update_timeout                            = "5m"
  delete_timeout                            = "10m"

  tags = {
    Name        = "vpc-peering-pub2be"
    Project     = var.project
    Environment = var.environment
    Module      = var.module
  }
}

```
<br />
***


**brikis98** commented *May 3, 2021*

I think this may just be a limitation of `run-all plan` and mock values in Terragrunt. [See the warning here](https://terragrunt.gruntwork.io/docs/reference/cli-options/#run-all).
***

