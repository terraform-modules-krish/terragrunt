# How to securely setup DRY multienvironment terragrunt

**pbn4** commented *Oct 19, 2021*

Goal: I want to have a setup where I have a root module creating an IAM user for terragrunt to assume in number of environments (dev, staging, prod). Root module will be created with some root org IAM user credentials but other modules should be created by assuming a role in a specific env (because I want each env to live in a separate AWS account). I also want to have those credentials propagated automatically to `dev`/`staging`/`prod` modules.

My approach at the moment:
I have a root organization in which I'm creating an organizational unit `dev` and an IAM user `terraform` who can sts:AssumeRole in `dev` (OrganizationAccountAccessRole):

root/main.tf: 

```
terraform {
  backend "s3" {

  }
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "3.63.0"
    }
  }
}

resource "aws_organizations_organization" "organization" {
  aws_service_access_principals = [
    "cloudtrail.amazonaws.com",
    "config.amazonaws.com",
  ]

  feature_set = "ALL"
}

resource "aws_organizations_organizational_unit" "unit_dev" {
  name      = "dev"
  parent_id = aws_organizations_organization.organization.roots[0].id
}

resource "aws_organizations_account" "unit_dev_account" {
  name  = "name"
  email = "myemail@example.com"
  parent_id = aws_organizations_organizational_unit.unit_dev.id
  iam_user_access_to_billing = "DENY"
}

resource "aws_iam_user" "terraform_user" {
  name = "terraform"
  path = "/cicd/"
}

resource "aws_iam_access_key" "terraform_user" {
  user    = aws_iam_user.terraform_user.name
}

resource "aws_iam_user_policy" "terraform_user_assume_organization_role_dev" {
  name = "AssumeOrganizationRoleDev"
  user = aws_iam_user.terraform_user.name

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": [
        "sts:AssumeRole"
      ],
      "Effect": "Allow",
      "Resource": "arn:aws:iam::${aws_organizations_account.unit_dev_account.id}:role/OrganizationAccountAccessRole"
    }
  ]
}
EOF
}

output "unit_dev_account" {
  value = aws_organizations_account.unit_dev_account.id
}

output "terraform_user_name" {
  value = aws_iam_user.terraform_user.name
}

output "terraform_user_access_keys" {
  value = aws_iam_access_key.terraform_user
  sensitive = true
}
```

Now in `dev/terragrunt.hcl`: 

```
locals {
  env = "dev"
  aws_region = "eu-west-1"
  aws_profile = "my-profile"
}

dependency "root_org" {
  config_path = "${get_parent_terragrunt_dir()}/../root"
}

remote_state {
  backend = "s3"
  config = {
    bucket         = "terraform-state-geocargo-${local.env}"
    key            = "${path_relative_to_include()}/terraform.tfstate"
    region         = local.aws_region
    encrypt        = true
    dynamodb_table = "terraform-lock-table"
    profile        = local.aws_profile
  }
}

generate "provider" {
  path = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents = <<EOF
provider "aws" {
    access_key = "${dependency.root_org.outputs.terraform_user_access_keys.id}"
    secret_key = "${dependency.root_org.outputs.terraform_user_access_keys.secret}"
     assume_role {
      role_arn     = "arn:aws:iam::${dependency.root_org.outputs.unit_dev_account}:role/OrganizationAccountAccessRole"
      session_name = "terragrunt-session"
    } 
    region  = "${local.aws_region}"
}
EOF
}
```

The downsides of this solution:

- access_key and secret_key are written to the disk into provider.tf file,
- s3 configuration in dev still uses a profile used to create root module (I was not able to configure it using assume role with secrets created in root module)

Are there any better approaches to this? I'm planning to run `terragrunt apply` on Codebuild in the future which I can encrypt underlying storage on with KMS. Maybe writing secrets to disks isn't really such a big deal here? 
<br />
***


**yorinasub17** commented *Oct 19, 2021*

I don't think the current mechanisms that Terragrunt exposes is going to support this. Supporting this is most definitely going to need an overhaul of the cloud authentication functionality (https://github.com/gruntwork-io/terragrunt/issues/1840).

Terragrunt basically only has features for assuming a role, but it does not have any feature for switching IAM users, which is what it sounds like you need. We are most likely not going to implement anything like that without overhauling the cloud auth mechanisms.

With that said, if you are looking for a resolution for just the provider authentication (and not the s3 config), one thing you can do is to store the credentials in secrets manager and dump data sources:

```hcl
generate "provider" {
  path = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents = <<EOF
data "aws_secretsmanager_secret_version" "access_key" {
  secret_id = "${dependency.root_org.outputs.terraform_user_access_keys_secrets_manager_arn}"
}

provider "aws" {
    access_key = jsondecode(data.aws_secretsmanager_secret_version.access_key.secret_string).access_key
    secret_key = jsondecode(data.aws_secretsmanager_secret_version.access_key.secret_string).secret_key
     assume_role {
      role_arn     = "arn:aws:iam::${dependency.root_org.outputs.unit_dev_account}:role/OrganizationAccountAccessRole"
      session_name = "terragrunt-session"
    } 
    region  = "${local.aws_region}"
}
EOF
}
```



***

**pbn4** commented *Oct 20, 2021*

Thanks for answering, using data source secrets manager in provider configuration is a great idea. :) 
***

**yorinasub17** commented *Oct 20, 2021*

Closing as resolved! Please follow #1840 to be notified when we overhaul cloud authentication in general, which should hopefully make this use case better.
***

