# Terragrunt + Github Actions error when using mock_outputs

**lpossamai** commented *May 16, 2023*

**Describe the bug**
When using a `dependency` block with `mock_outputs`, works locally but not in CI.

Current Terragrunt structure is:
```
.
├── terragrunt.hcl
├── README.md
├── environments
│   ├── staging
│   │   └── env.hcl
│   ├── prod
│   │   └── env.hcl
│   ├── dev
│   │   ├── Makefile
│   │   ├── env.hcl
│   │   └── ap-southeast-2
│   │       ├── s3-logging-bucket
│   │       │   ├── terragrunt.hcl
│   │       │   └── Makefile
│   │       ├── s3
│   │       │   ├── terragrunt.hcl
│   │       │   └── Makefile
│   │       └── region.hcl
└── config
    ├── Makefile
    └── _envcommon
        ├── s3-logging-bucket.hcl
        ├── s3.hcl
        └── README.md
```

`_envcommon/s3.hcl`:
```hcl
locals {
  # Automatically load environment-level variables
  environment_vars = read_terragrunt_config(find_in_parent_folders("env.hcl"))

  # Automatically load region-level variables
  region_vars = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  aws_region  = local.region_vars.locals.aws_region

  # Extract out common variables for reuse
  env = local.environment_vars.locals.environment

  # Expose the base source URL so different versions of the module can be deployed in different environments.
  base_source_url = "tfr:///terraform-aws-modules/s3-bucket/aws?version=3.10.1"
}

dependency "s3_access_logs" {
  config_path = "${dirname(find_in_parent_folders())}/environments/${local.env}/${local.aws_region}/s3-logging-bucket"

  mock_outputs = {
    s3_bucket_id = "temporary-dummy-bucket"
  }
  mock_outputs_allowed_terraform_commands = ["validate"]
}

inputs = {
  bucket = "test-lpossamai-s3-bucket-${local.env}"

  versioning = {
    status     = true
    mfa_delete = false
  }

  logging = {
    target_bucket = dependency.s3_access_logs.outputs.s3_bucket_id
    target_prefix = "${local.env}/"
  }

  # S3 bucket-level Public Access Block configuration
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true

  # Bucket policies
  attach_deny_insecure_transport_policy = true
  attach_require_latest_tls_policy      = true
  attach_elb_log_delivery_policy        = true

  server_side_encryption_configuration = {
    rule = {
      apply_server_side_encryption_by_default = {
        sse_algorithm = "aws:kms"
      }
    }
  }
}
```

`_envcommon/s3-logging-bucket.hcl`:
```hcl
locals {
  # Automatically load environment-level variables
  environment_vars = read_terragrunt_config(find_in_parent_folders("env.hcl"))

  # Automatically load region-level variables
  region_vars = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  aws_region  = local.region_vars.locals.aws_region

  # Extract out common variables for reuse
  env = local.environment_vars.locals.environment

  # Expose the base source URL so different versions of the module can be deployed in different environments.
  base_source_url = "tfr:///terraform-aws-modules/s3-bucket/aws?version=3.10.1"
}

inputs = {
  bucket = "test-lpossamai-s3-bucket-log-${local.env}"

  acl = "log-delivery-write"

  versioning = {
    status     = false
    mfa_delete = false
  }

  server_side_encryption_configuration = {
    rule = {
      apply_server_side_encryption_by_default = {
        sse_algorithm = "aws:kms"
      }
    }
  }
}
```

Running `terragrunt run-all validate` locally:
```
Group 1
- Module /terragrunt/examples/environments/dev/ap-southeast-2/s3-logging-bucket

Group 2
- Module /terragrunt/examples/environments/dev/ap-southeast-2/s3

WARN[0000] No double-slash (//) found in source URL /terraform-aws-modules/s3-bucket/aws. Relative paths in downloaded Terraform code may not work.  prefix=[/terragrunt/examples/environments/dev/ap-southeast-2/s3-logging-bucket]
Success! The configuration is valid.

WARN[0003] No double-slash (//) found in source URL /terraform-aws-modules/s3-bucket/aws. Relative paths in downloaded Terraform code may not work.  prefix=[/terragrunt/examples/environments/dev/ap-southeast-2/s3-logging-bucket]
WARN[0007] No double-slash (//) found in source URL /terraform-aws-modules/s3-bucket/aws. Relative paths in downloaded Terraform code may not work.  prefix=[/terragrunt/examples/environments/dev/ap-southeast-2/s3]
Success! The configuration is valid.
```

Github Actions error:
```
time=2023-05-16T05:08:53Z level=debug msg=[Partial] Included config /terragrunt/example/terragrunt.hcl has strategy shallow merge: merging config in (shallow). prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket] 
time=2023-05-16T05:08:53Z level=warning msg=No double-slash (//) found in source URL /terraform-aws-modules/s3-bucket/aws. Relative paths in downloaded Terraform code may not work. prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket] 
time=2023-05-16T05:08:53Z level=debug msg=Detected module /terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl is already init-ed. Retrieving outputs directly from working directory. prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket] 
time=2023-05-16T05:08:53Z level=debug msg=Running command: terraform output -json prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket] 
-05-16T05:08:57Z level=debug msg=Retrieved output from /terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl as json: [command]/home/runner/work/_temp/fdda73fc-de37-4330-a2f7-31c1af8e2bf1/terraform-bin output -json
{}
 prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket] 
time=2023-05-16T05:08:57Z level=error msg=Module /terragrunt/example/environments/dev/ap-southeast-2/s3 has finished with an error: Could not parse output from terragrunt config /terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl. Underlying error: invalid character ':' after top-level value prefix=[/terragrunt/example/environments/dev/ap-southeast-2/s3] 
time=2023-05-16T05:08:57Z level=error msg=1 error occurred:
	* Could not parse output from terragrunt config /terragrunt/example/environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl. Underlying error: invalid character ':' after top-level value
```

**Versions**
- Terragrunt version: `v0.45.11`
- Terraform version: `1.4.6`
- Environment details (Ubuntu 20.04, Windows 10, etc.): Locally is `PopOS` and Github Actions uses `ubuntu-latest`

**Additional context**
Yes, I know running `run-all` on a production environment [is not recommended](https://github.com/gruntwork-io/terragrunt/issues/720#issuecomment-497888756). Please, [check my post](https://github.com/orgs/gruntwork-io/discussions/709#discussion-5179702) in the discussion section to understand more.
<br />
***


**levkohimins** commented *May 18, 2023*

@lpossamai, are you using `--terragrunt-non-interactive` with `run-all` and what directory are you running `terragrunt` from?

Notice if this message is present in the logs
```
The --non-interactive flag is set. To avoid accidentally affecting external dependencies with a run-all command, will not run this command against module ...
```

If so, then the solution is to run `terragrunt` from the parent directory, i.e. `/terragrunt/example/environments/dev`
***

**lpossamai** commented *May 18, 2023*

hi @levkoburburas - Yep, `--non-interactive` is set and I am running `run-all` from the parent directory.
***

**denis256** commented *May 19, 2023*

Hello,
it is complicated to reproduce this issue without an example code, I tried to arrange mentioned  code to [files](https://github.com/denis256/terragrunt-tests/tree/master/issue-2569) but the issue is not happening
***

**lpossamai** commented *May 21, 2023*

> Hello, it is complicated to reproduce this issue without an example code, I tried to arrange mentioned code to [files](https://github.com/denis256/terragrunt-tests/tree/master/issue-2569) but the issue is not happening

Here are the config files:

Repository structure:
``` 
├── config
│   ├── _envcommon
│   │   ├── README.md
│   │   ├── s3.hcl
│   │   └── s3-logging-bucket.hcl
│   ├── Makefile
├── environments
│   ├── dev
│   │   ├── ap-southeast-2
│   │   │   ├── region.hcl
│   │   │   ├── s3
│   │   │   │   ├── Makefile
│   │   │   │   └── terragrunt.hcl
│   │   │   └── s3-logging-bucket
│   │   │       ├── Makefile
│   │   │       └── terragrunt.hcl
│   │   └── env.hcl
│   ├── prod
│   │   └── env.hcl
│   └── staging
│       └── env.hcl
├── README.md
└── terragrunt.hcl
```

`environments/dev/ap-southeast-2/s3/terragrunt.hcl`:
```
terraform {
  source = "${include.envcommon.locals.base_source_url}"
}

include "root" {
  path = find_in_parent_folders()
}

include "envcommon" {
  path   = "${dirname(find_in_parent_folders())}/config/_envcommon/s3.hcl"
  expose = true
}

inputs = {}
```

`environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl`:
```
terraform {
  source = "${include.envcommon.locals.base_source_url}"
}

include "root" {
  path = find_in_parent_folders()
}

include "envcommon" {
  path   = "${dirname(find_in_parent_folders())}/config/_envcommon/s3.hcl"
  expose = true
}

inputs = {}
```

`environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl`:
```
terraform {
  source = "${include.envcommon.locals.base_source_url}"
}

include "root" {
  path = find_in_parent_folders()
}

include "envcommon" {
  path   = "${dirname(find_in_parent_folders())}/config/_envcommon/s3.hcl"
  expose = true
}

inputs = {}
```

`environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl`:
```
terraform {
  source = "${include.envcommon.locals.base_source_url}"
}

include "root" {
  path = find_in_parent_folders()
}

include "envcommon" {
  path   = "${dirname(find_in_parent_folders())}/config/_envcommon/s3-logging-bucket.hcl"
  expose = true
}

inputs = {}
```

`config/_envcommon/s3.hcl`:
```
locals {
  # Automatically load environment-level variables
  environment_vars = read_terragrunt_config(find_in_parent_folders("env.hcl"))

  # Automatically load region-level variables
  region_vars = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  aws_region  = local.region_vars.locals.aws_region

  # Extract out common variables for reuse
  env = local.environment_vars.locals.environment

  # Expose the base source URL so different versions of the module can be deployed in different environments.
  base_source_url = "tfr:///terraform-aws-modules/s3-bucket/aws//.?version=3.10.1"
}

dependency "s3_access_logs" {
  config_path = "${dirname(find_in_parent_folders())}/environments/${local.env}/${local.aws_region}/s3-logging-bucket"

  # Configure mock outputs for the `validate` command that are returned when there are no outputs available (e.g the
  # module hasn't been applied yet.
  mock_outputs_allowed_terraform_commands = ["init", "validate", "apply", "plan", "destroy", "output"]

  mock_outputs = {
    s3_bucket_id = "temporary-dummy-bucket"
  }
}

inputs = {
  bucket = "lucas-example-bucket-${local.env}"

  versioning = {
    status     = true
    mfa_delete = false
  }

  logging = {
    target_bucket = dependency.s3_access_logs.outputs.s3_bucket_id
    target_prefix = "${local.env}/"
  }

  # S3 bucket-level Public Access Block configuration
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true

  # Bucket policies
  attach_deny_insecure_transport_policy = true
  attach_require_latest_tls_policy      = true
  attach_elb_log_delivery_policy        = true

  server_side_encryption_configuration = {
    rule = {
      apply_server_side_encryption_by_default = {
        sse_algorithm = "aws:kms"
      }
    }
  }
}

```

`config/_envcommon/s3-logging-bucket.hcl`:
```
locals {
  # Automatically load environment-level variables
  environment_vars = read_terragrunt_config(find_in_parent_folders("env.hcl"))

  # Automatically load region-level variables
  region_vars = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  aws_region  = local.region_vars.locals.aws_region

  # Extract out common variables for reuse
  env = local.environment_vars.locals.environment

  # Expose the base source URL so different versions of the module can be deployed in different environments.
  base_source_url = "tfr:///terraform-aws-modules/s3-bucket/aws//.?version=3.10.1"
}

inputs = {
  bucket = "lucas-example-bucket-log-${local.env}"

  versioning = {
    status     = false
    mfa_delete = false
  }

  server_side_encryption_configuration = {
    rule = {
      apply_server_side_encryption_by_default = {
        sse_algorithm = "aws:kms"
      }
    }
  }
}
```

Then: `cd environments/dev && terragrunt run-all plan --terragrunt-non-interactive`

But, again. It works locally but it does not work on a CI (Github Actions).
***

**denis256** commented *May 22, 2023*

Hello,
I suspect it is related to how Terragrunt is installed since in logs is mentioned `terraform-bin` instead of `terraform`

```
[command]/home/runner/work/_temp/fdda73fc-de37-4330-a2f7-31c1af8e2bf1/terraform-bin output -json
```

In my tests using Tgenv, Tfenv is invoked `terraform` binary and it is passing successfully:
```
time=2023-05-22T20:07:08Z level=debug msg=Running command: terraform output -json prefix=[/github/workspace/issue-2569-2/environments/dev/ap-southeast-2/s3-logging-bucket] 
time=2023-05-22T20:07:10Z level=debug msg=Retrieved output from /github/workspace/issue-2569-2/environments/dev/ap-southeast-2/s3-logging-bucket/terragrunt.hcl as json: {}
 prefix=[/github/workspace/issue-2569-2/environments/dev/ap-southeast-2/s3-logging-bucket] 
```

https://github.com/denis256/terragrunt-tests/actions/runs/5049933706/jobs/9060020410#step:4:872

If is used GH action `hashicorp/setup-terraform`, try to disable wrapper script
```
steps:
- uses: hashicorp/setup-terraform@v2
  with:
    terraform_wrapper: false
```

***

**jagoodhand** commented *Jun 8, 2023*

I've just experienced the same error.

Without the `terraform_wrapper: false` line in the `setup-terraform` action, I was getting the error:

> Underlying error: invalid character ':' after top-level value prefix

After setting `terraform_wrapper: false`, this revealed the 'true' error:

> Unsupported attribute; This object does not have an attribute named "code_bucket_id".

```hcl
dependency "code_deployment_infra" {
  config_path = "../code_deployment_infra"

  mock_outputs = {
    code_bucket_id = "my_bucket_name"
  }
}
```

Here though, the state did exist for the `code_bucket_id` value, as this had already been deployed in a previous run whilst I was developing. 

I tried setting:

`mock_outputs_merge_strategy_with_state = "shallow"`

And this then cleared the error, and the plan was successful. Hopefully this helps to pinpoint where the bug is?

Terraform version: 1.4.6
Terragrunt version: v0.46.1/terragrunt_linux_amd64

***

