# Terragrunt plan-all fails for modules with dependency and remote state outputs.

**esn89** commented *Jul 3, 2020*

I have a module called "NAT" which I wish to deploy, it is located here:
`/project/gcp/network/production/us/nat/terragrunt.hcl`

and it contains:

```
terraform {
  source = "../../../modules/natgw"

  extra_arguments "credentials" {
    commands = get_terraform_commands_that_need_vars()
    arguments = [
      "-var",
      "credentials=${get_terragrunt_dir()}/${find_in_parent_folders("credentials.json")}",
      "-var",
      "state_bucket_prefix=${get_env("PROJECT", "NULL")}",
      "-var-file",
      "${find_in_parent_folders("common.tfvars")}",
    ]
  }
}

include {
  path = find_in_parent_folders()
}

dependency "vpc" {
  config_path = "../vpc"
  skip_outputs = true
  mock_outputs = {
        network_self_link = "test"
  }
  mock_outputs_allowed_terraform_commands = ["validate", "plan"]
}

inputs = {
  network = dependency.vpc.outputs.network_self_link
}
```

The main.tf for this looks like this and is located in: `/project/gcp/network/modules/nat/main.tf`
```
resource "google_compute_router" "nat_router" {
  name    = "natgateway"
  network = data.terraform_remote_state.network.outputs.network_self_link
  project = data.terraform_remote_state.projects.outputs.project_id
  region  = var.region
}
```

As you can see it depends on the VPC module which I have NOT yet deployed.
The VPC module (once it is deployed) itself contains some outputs such as the needed "network_self_link".

Whenever, I run `terragrunt plan-all` on this NAT Gateway module, I always experience this:

```
Error: Unsupported attribute

  on main.tf line 4, in resource "google_compute_router" "nat_router":
   4:   network = data.terraform_remote_state.network.outputs.network_self_link
    |----------------
    | data.terraform_remote_state.network.outputs is object with no attributes

This object does not have an attribute named "network_self_link".

[terragrunt] 2020/07/02 20:59:49 Hit multiple errors:
```

I've read the doc and it says that I should use `skip_outputs = true` to prevent the NAT Gateway module from reading the VPC remote state outputs because it has not yet been created, which I did, but I am still experiencing this behaviour.

The behaviour that I expect to happen is that:  The plan should continue with my mock outputs AND during the apply stage, it will automatically apply the VPC module, THEN the NAT Gateway module.
<br />
***


**brikis98** commented *Jul 4, 2020*

> ```
> Error: Unsupported attribute
> 
>   on main.tf line 4, in resource "google_compute_router" "nat_router":
>    4:   network = data.terraform_remote_state.network.outputs.network_self_link
>     |----------------
>     | data.terraform_remote_state.network.outputs is object with no attributes
> 
> This object does not have an attribute named "network_self_link".
> ```

This is a Terraform error, not a Terragrunt error. It suggests you have a `terraform_remote_state` data source somewhere trying to read state that doesn't exist yet. If you're using `dependency` blocks, you can probably remove the `terraform_remote_state` resources, right?
***

**esn89** commented *Jul 4, 2020*

Hi @brikis98 

Yes, I have a `backend.tf` in my modules folder which contains all of these.

My understanding is that with terragrunt it won't read it.  But I probably misunderstood.  So what I have to do during the `terragrunt plan-all` is to comment out the everything in my `backend.tf` file, and then run the `terragrunt plan-all` command again.  

I thought the `skip_outputs` was responsible for making the remote states not be read.

If I have dependency blocks, you mean I can skip the terraform_remote_state and have these output variables be grabbed from the other module? 
***

**brikis98** commented *Jul 5, 2020*

`plan-all` runs `plan` in each module, so if that module has `terraform_remote_state` data sources, those will get executed. The `skip_outputs` setting only affects how `dependency` blocks are processed, not whether `plan` is executed in each module or not.

One of the ways to use `dependency` blocks is to have Terragrunt read outputs from other modules, so you can pass those in as inputs to another module. This is often used as an alternative to `terraform_remote_state`.  So you could replace the `terraform_remote_state` usage with Terragrunt `dependency` blocks, set `skip_outputs` on those, and even provide mock outputs, and `plan-all` should work.
***

**esn89** commented *Jul 6, 2020*

Thank you @brikis98

I am trying this right now and one of my modules, located in: 
`network/modules/vpc/outputs.tf` has an output which looks like:

```
output "subnets" {
  value = var.subnets
}

output "network_self_link" {
  value       = module.vpc.network_self_link
}
```

And in the NAT module which depends on VPC has a `terragrunt.hcl` which looks like:
```
dependency "project" {
  config_path = "../../../../resources/dev/us/project"

  skip_outputs = true

  mock_outputs = {
    network = "test_value"
  }
  mock_outputs_allowed_terraform_commands = ["validate", "plan"]
}

dependency "vpc" {
  config_path = "../vpc"

  skip_outputs = true

  mock_outputs = {
    cloud_nat_subnets = ["10.0.1.2", "10.0.1.3"]
    network           = "vpc.network.test"
  }
  mock_outputs_allowed_terraform_commands = ["validate", "plan"]
}

inputs = {
  network           = dependency.vpc.outputs.network_self_link
  cloud_nat_subnets = dependency.vpc.outputs.cloud_nat_subnets
  project           = dependency.project.outputs.project_id
}
```

However, when I do `terragrunt plan-all` it still says: ```Encountered the following errors:
/network/dev/us/nat/terragrunt.hcl:48,45-63: Unsupported attribute; This object does not have an attribute named "network_self_link"., and 1 other diagnostic(s)```

I am not sure what I am doing wrong.  I am not using the `data.terraform_remote_state` in my code anymore, I am using the outputs from modules like you showed me, but it still complains about that output when the output is declared in VPC's outputs.tf


 SOLVED:  turns out my `network           = "vpc.network.test"` should be `network_self_link           = "vpc.network.test"`
***

**brikis98** commented *Jul 7, 2020*

Alright, sounds like you got this resolved!
***

