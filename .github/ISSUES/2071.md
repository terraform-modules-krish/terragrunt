# Generate complex values

**twang817** commented *Apr 14, 2022*

I'm using generate blocks to inject some constants into my terraform plans.  Simple example:

```
# terragrunt.hcl
locals {
  prefix = "myprefix-"
}

generate "locals" {
  path = "locals.tf"
  if_exists = "error"
  contents = <<-EOF
    locals {
      prefix = "${local.prefix}"
    }
    EOF
}
```

While this simple examples, if I wanted to pass some more complex value, ie:

```
locals {
   complex = {
     foo = 1
     bar = {
        baz = "batz"
     }
   }
}

generate "locals" {
  # ...
  contents = <<-EOF
    locals {
      complex = ???
    }
    EOF
```

For a simple list or map, one could maybe do something like this:

```
foo = [
  %{~ for i in local.some_list ~}
  "${i}",
  %{~ endfor ~}
]
```

But, I couldn't find an easy way to do this for an arbitrarily deep nested object.

It seems this should be relatively straightforward with some sort of tohcl() function that will take an arbitrary object and output the proper hcl.

Does anyone have any neat tricks?  Is anyone interested in a PR?
<br />
***


**denis256** commented *Apr 14, 2022*

Hi,
I was thinking about using `jsonencode` to pass encoded complex variable:

```
# terragrunt.hcl

locals {
  prefix = "myprefix-"
  complex = {
    foo = 1
    bar = {
      baz = "batz"
    }
  }
}

generate "locals" {
  if_exists = "overwrite"
  path      = "qwe.tf"
  contents = <<-EOF
    locals {
      complex1 = ${jsonencode(local.complex)}
    }

    resource "local_file" "file" {
      content     = "foo: $${local.complex1.foo}  bar: $${local.complex1.bar.baz}"
      filename = "app.txt"
    }
    EOF
}

```

Result in `app.txt` after apply:
```
foo: 1  bar: batz
```
***

**ybltv** commented *Jun 12, 2022*

You should consider using [JSON syntax](https://www.terraform.io/language/syntax/json) for that. Do not forget to specify `disable_signature = true` property in your `generate` block as that creates a comment, and JSON doesn't support comments 

Then, you could do something like:

```
generate "locals" {
  path = "locals.tf.json"
  disable_signature = true
  if_exists = "overwrite"
  contents = jsonencode({
    locals = {
      complex = local.complex
      simple = local.simple
    }
  })
}
```

Even better, you could then put all the values you want to inject into a single Terragrunt variable and then just do something like `local = local.values_to_inject` in the example above
***

