# Generate provider.tf with provider version dependent on terraform version

**jowoseni-deloitte** commented *Nov 11, 2020*

We are able to generate provider.tf file as below.

```
generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
provider "aws" {
  version = "~> 3.6"
  }
}
EOF
}
```
However we want to generate provider.tf with different provider version based on terraform version such as the below;

```
  generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
%{ if terraform_version == 0.13 }
terraform {
  required_providers {
    aws {
      version = "~> 3.6"
    }
  }
}
%{ endif }
provider "aws" {
%{ if terraform_version != 0.13 }
  version = "~> 3.5"
%{ endif }
}
EOF
}
```

This approach does not seems to work. Is there a work around to achieve the functionality.
<br />
***


**yorinasub17** commented *Nov 11, 2020*

You can only reference things in `locals` (via `local.NAME`) or one of the [built-in functions](https://terragrunt.gruntwork.io/docs/reference/built-in-functions/) in terragrunt interpolation. Given that, this is currently not possible because we don't currently have a built in function to expose the running terraform version. That said, a function to expose that seems reasonable to add here.

In the meantime, a potential workaround here would be to use `run_cmd` to make the `terraform --version` call.
***

**dhoppe** commented *Jul 16, 2021*

I wanted to use ```run_cmd("terraform", "version")```, but since Terraform 0.14 the output has changed. For that reason I have chosen a different path. 

```
locals {
  terraform_version = trimspace(run_cmd("cat", "${find_in_parent_folders(".terraform-version")}"))
}

generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
provider "hcloud" {
  token = "${local.hcloud_token}"
}
  EOF
}

generate "versions" {
  path      = "versions.tf"
  if_exists = "overwrite"
  contents  = <<EOF
terraform {
  required_providers {
%{if local.terraform_version == "0.12.31"~}
    hcloud = ">= 1.23"
%{else~}
    hcloud = {
      source  = "hetznercloud/hcloud"
      version = ">= 1.23"
    }
%{endif~}
  }

  required_version = ">= 0.12"
}
EOF
}
```
***

